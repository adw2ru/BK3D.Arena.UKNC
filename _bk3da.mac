	.TITLE MAIN
	.LIST MEB
	.ENABL LC

	.ASECT
	.=1000

.MACRO RET
	RETURN
.ENDM
C_VADDR=40020
C_MADDR=110020
C_XADDR=122260
C_TADDR=100010
C_SCRWID=160.
C_SCRWIH=80.
SCOREP=C_TADDR+54
AMMOP=C_TADDR+40
MSGLN=C_TADDR+3220
FNT=117430
V_SC0=0.
V_SC1=1.
H_SND=2.
A_SND=3.
F_SND=4.
F_IVTS=5.
F_EXIT=6.
F_PCHR=7.
F_PMSG=8.
F_TMR=9.
V_SC2=10.
M_STA=11.
MNRS=12.
HITRAD=19.
SCLIM=25.
GSEXIT=0.
GSGAME=1.
GSPAUS=2.
GSPLWN=4.
GSHTWN=8.

START:	MTPS #200

	MOV #1,@#PpuComm

	MOV #PPUBEG,R4
	MOV #PPUEND-PPUBEG/2,R5
	CALL PPRUN
	TST @#PpuComm
	BNE .-4

	CLR BITFLD
	CLR AMAP
	CALL CLWHLE
	MOV @#100,(PC)+
OLDTMI:	.WORD 0
	MOV @#102,(PC)+
OLDTMM:	.WORD 0
	MOV #NEWTMI,@#100
	MOV #200,@#102
	MOV #F_TMR,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	MTPS #0
	JMP GOTIT
NEWTMI:	BIT BITFLD,#8.
	BEQ 1$
	BIT SKIP,#1
	BNE 1$
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	MOV @#176640,-(SP)
	CALL GLOGIC
	MOV (SP)+,@#176640
	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
1$:	INC (PC)+
SKIP:	.WORD 0
	CMP SKIP,#50.
	BLO 2$
	CLR SKIP
	BIS #128.,BITFLD
2$:	RTI
SCH2:	MFPS -(SP)
	MTPS #200
	TST @#PpuComm
	BNE .-4
	MOV #1,@#PpuComm
	BIT #128.,@#176674
	BEQ .-6
	MOVB R0,@#176676
	BIT #128.,@#176674
	BEQ .-6
	MTPS (SP)+
	RET
CLWHLE:	MOV #40000,R0
	MOV #15440.,R3
	CLR (R0)+
	SOB R3,.-2
CLVRAM:	MOV #100000,@#176640
	MOV #20480.,R3
1$:	CLR @#176642
	INC @#176640
	SOB R3,1$
	RET
GSTATE:	.WORD 0
BITFLD:	.WORD 0
AMMO:	.BYTE 10.
OLDAM:	.BYTE 1.
SCORE:	.BYTE 0.,0.
OLDSCR:	.BYTE 1.,0.
FRCNTR:	.BYTE 0
	.EVEN
PLX:	.WORD 0
PLY:	.WORD 0
PLA:	.WORD 0
SPLX:	.WORD 0
SPLY:	.WORD 0
SPLA:	.WORD 0
PROCSK:	.WORD KeyLeft,KeyRight,KeyUp,KeyDown,KeySpace
	.WORD KeyJ,KeyF,KeyC,KeyW,KeyY
	.WORD KeyK1,KeyK2,KeyK3,KeyK5,KeyEnter,KeyStop
PROCSP:	.WORD DLFT,DRGHT,DUP,DDWN,DSPC
	.WORD DROT,DSL,DUP,DSR,DDWN
	.WORD DTAB,DPAUS,DROT,DEXIT,DREST,DFPS
PROCSR:	.WORD 177772,177772,177772,177772,177772
	.WORD 177772,177772,177772,177772,177772
	.WORD 177760,177774,177772,177760,177763,177772
PROCSC:	.WORD 0,0,0,0,1
	.WORD 1,0,0,0,0
	.WORD 1,1,1,1,1,1
PROCST:	.WORD 2,2,10,10,0
	.WORD 0,4,10,4,10
	.WORD 0,0,0,0,0,0
WASP:	.WORD 0
ANGV:	.WORD 0
JKB:	CLR R0
1$:	MOV PROCSK(R0),R1
	TST (R1)
	BEQ 2$
	TST PROCSC(R0)
	BEQ 3$
	CLR (R1)
3$:	MOV GSTATE,R1
	BIC PROCSR(R0),R1
	BEQ 2$
	BIS PROCST(R0),WASP
	MOV PROCSP(R0),R1
	MOV R0,-(SP)
	CALL (R1)
	MOV (SP)+,R0
2$:	TST (R0)+
	CMP R0,#32.
	BNE 1$
	RET
FMS:	MOV FMSP,R2
	INC R2
	BIC #177774,R2
	MOV R2,FMSP
	MOVB R0,FMSD1(R2)
	MOVB R1,FMSD2(R2)
	CLR R0
	CLR R1
	MOV #4.,R2
1$:	MOVB FMSD1-1(R2),R3
	ADD R3,R0
	MOVB FMSD2-1(R2),R3
	ADD R3,R1
	SOB R2,1$
	ASR R0
	ASR R0
	ASR R0
	ASR R1
	ASR R1
	ASR R1
	RET
FMSP:	.WORD 0
FMSD1:	.BLKB 4.
FMSD2:	.BLKB 4.
	.EVEN
MAKEMM:	TST R1
	BEQ 1$
	BPL 2$
	NEG R1
	MOV R4,R5
2$:	CMP R1,#8.
	BLOS 3$
	MOV #8.,R1
3$:	MOV R1,-(SP)
	CALL GETDIR
	MOV #9.,R2
	SUB (SP)+,R2
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R0,-(SP)
	MOV R2,-(SP)
	MOV (SP)+,R4
	MOV (SP)+,R2
	BPL 11$
	NEG R2
	CALL SPDIV
	NEG R2
	BR 12$
11$:	CALL SPDIV
12$:	MOV R2,R0
	MOV (SP)+,R4
	MOV (SP)+,R2
	BPL 21$
	NEG R2
	CALL SPDIV
	NEG R2
	BR 22$
21$:	CALL SPDIV
22$:	MOV R2,R1
	CALL (R5)
1$:	RET
JMS:	MOV GSTATE,R1
	BIC PROCSR,R1
	BEQ 4$
        MOV MSST,R1
	BIC #256.+1.,R1
	MOV R1,R0
	SWAB R0
	ASRB R0
	ASRB R1
	CALL FMS
	MOV R0,-(SP)
	TST KeyShift
	BNE 22$
	BIT #2,WASP
	BNE 22$
	ADD R1,PLA
	BIC #177000,PLA
	BR 21$
22$:	BIT #4,WASP
	BNE 21$
	MOV #DSL2,R4
	MOV #DSR2,R5
	CALL MAKEMM
21$:	MOV (SP)+,R1
	BIT #10,WASP
	BNE 4$
	MOV #DDWN2,R4
	MOV #DUP2,R5
	CALL MAKEMM
4$:	RET
JPS:	BIT WASP,#1
	BNE 1$
	CLR ANGV
1$:	RET
JOY:	CLR WASP
	CALL JKB
	CALL JMS
	CALL JPS
	RET
IFRC:	MOVB FRCNTR,R0
	INCB R0
	INCB R0
	MOVB R0,FRCNTR
	BNE 1$
	CALL CLVIS
1$:	RET
GLOGIC:	CMP #GSPAUS,GSTATE
	BEQ 1$
	CALL MNMOV
1$:	CALL JOY
	CMP #GSHTWN,GSTATE
	BNE 2$
	CALL SETTA
	MOV MNBF+6.,R1
	MOV MNBF+4.,R0
	CALL SETPL
2$:	RET
THSNC:	MTPS #200
	MOV PLA,SPLA
	MOV PLX,SPLX
	MOV PLY,SPLY
	CALL MNSYNC
	MTPS #0
	RET
LMAP:	CLRB FRCNTR
	CALL PREPM
	CALL CLVIS
	CALL MNSET
	CALL PREPR
	CALL INISCR
	CALL INIAM
	RET
GO:
	CALL PREP0
	CALL CLPSC
	CALL DRST2
	BIS #8.,BITFLD
ML:	CALL THSNC
	CALL IFRC
	CALL RENDR
	CALL MNPRC
	CALL DRDIFF
	CALL PRNSCR
	CALL PRNAM
	INC (PC)+
CFPS:	.WORD 0
	CALL PRFPS
ML2:
	CALL DOMAP
	CALL DORST
	CALL DOMSG
	MOV GSTATE,R0
	BEQ 1$
	ASR R0
	BEQ ML
	ASR R0
	BEQ ML2
	ASR R0
	BEQ ML
	BR ML
1$:	BIC #8.,BITFLD
	RET
PRFPS:	BIT #128.,BITFLD
	BEQ 1$
	BIC #128.,BITFLD
	BIT #256.,BITFLD
	BEQ 2$
	MOV #100000,R1
	MOV CFPS,R4
	CALL DOUT
2$:	CLR CFPS
1$:	RET
DORST:	BIT #4,BITFLD
	BEQ 1$
	BIC #4,BITFLD
	BIC #8.,BITFLD
	CALL DRST2
	BIS #8.,BITFLD
1$:	RET
DOMAP:	BIT #1,BITFLD
	BEQ 2$
	BIC #1,BITFLD
	BIT #2,BITFLD
	BNE 1$
	CALL CLRM
	RET
1$:	CALL DRAWM
	CALL MNDRW
	RET
2$:	BIT #2,BITFLD
	BEQ 3$
	CMP #GSPAUS,GSTATE
	BEQ 3$
	CALL MNDRW
3$:	RET
DOMSG:	BIT #16.,BITFLD
	BEQ 1$
	BIC #16.,BITFLD
	MOV #2,R5
	CALL PRNMS
	MOV #3,R5
	CALL PRNMS
1$:	BIT #32.,BITFLD
	BEQ 2$
	BIC #32.,BITFLD
	MOV #1,R5
	CALL PRNMS
	MOV #3,R5
	CALL PRNMS
2$:	BIT #64.,BITFLD
	BEQ 3$
	BIC #64.,BITFLD
	CLR R5
	CALL PRNMS
3$:	RET
DEXIT:	CLR GSTATE
	RET
DTAB:	MOV #2,R0
	MOV BITFLD,R1
	XOR R0,R1
	MOV R1,BITFLD
	BIS #1,BITFLD
1$:	RET
DFPS:	MOV #256.,R0
	MOV BITFLD,R1
	XOR R0,R1
	MOV R1,BITFLD
	BIT #256.,R1
	BNE 1$
	MOV #100000,@#176640
	MOV #32.,R1
3$:	MOV #8.,R0
2$:	CLR @#176642
	INC @#176640
	SOB R0,2$
	ADD #80.-8.,@#176640
	SOB R1,3$
1$:	RET
DSPC:	CALL DPROJ1
	RET
SETPL:	MOV R0,PLX
	MOV R1,PLY
	RET
GETDIR:	MOV PLA,R0
	CALL GSNCS
	RET
DDWN:	CALL GETDIR
DDWN2:	NEG R0
	NEG R1
	BR DUP2
DUP:	CALL GETDIR
DUP2:	MOV R0,R2
	MOV R1,R3
	ADD PLX,R0
	ADD PLY,R1
	CALL CHECK
	BNE 1$
	CALL SETPL
	BR 3$
1$:	SUB R3,R1
	CALL CHECK
	BNE 2$
	CALL SETPL
	BR 3$
2$:	ADD R3,R1
	SUB R2,R0
	CALL CHECK
	BNE 3$
	CALL SETPL
3$:	RET
DSL:	CALL GETDIR
DSL2:	XOR R0,R1
	XOR R1,R0
	XOR R0,R1
	NEG R1
	JMP DUP2
DSR:	CALL GETDIR
DSR2:	XOR R0,R1
	XOR R1,R0
	XOR R0,R1
	NEG R1
	JMP DDWN2
IANGV:	MOV ANGV,R0
	INC R0
	CMP R0,#6.
	BLO 1$
	MOV #5.,R0
1$:	MOV R0,ANGV
	ASL R0
	MOV IAVV(R0),R0
	BIS #1,WASP
	RET
IAVV:	.WORD 0.,3.,5.,6.,7.,8.
DLFT:	TST KeyShift
	BNE DSL
	CALL IANGV
	SUB R0,PLA
	BIC #177000,PLA
	RET
DRGHT:	TST KeyShift
	BNE DSR
	CALL IANGV
	ADD R0,PLA
	BIC #177000,PLA
	RET
DROT:	ADD #256.,PLA
	BIC #177000,PLA
	RET
DPAUS:	CMP GSTATE,#GSGAME
	BNE 2$
	MOV #GSPAUS,GSTATE
	BIS #64.,BITFLD
	BR 1$
2$:	CMP GSTATE,#GSPAUS
	BNE 1$
	MOV #GSGAME,GSTATE
	CALL CLMS
1$:	RET
DREST:	CMP GSTATE,#GSPLWN
	BEQ 1$
	CMP GSTATE,#GSHTWN
	BEQ 1$
	RET
1$:	BIS #4,BITFLD
	RET
DRST2:	CLR CFPS
	MOV (PC)+,R0
AMAP:	.WORD 0
	CALL LMAP
	MOV #GSGAME,GSTATE
	CALL CLMS
	BIT #2,BITFLD
	BEQ 1$
	CALL CLRM
	CALL DRAWM
1$:	CLR WASP
	CLR ANGV
	RET

CLMSK:
DRMS1:	.BYTE 3,3,14,14,60,60,300,300
DRMS2:	.BYTE 0,3,0,14,0,60,0,300
DRMS3:	.BYTE 3,0,14,0,60,0,300,0
SCTAB:	.WORD 040000,037776,037772,037764
	.WORD 037753,037740,037722,037702
	.WORD 037657,037632,037602,037550
	.WORD 037513,037454,037412,037346
	.WORD 037300,037227,037153,037075
	.WORD 037015,036732,036645,036555
	.WORD 036463,036366,036270,036166
	.WORD 036063,035755,035644,035532
	.WORD 035415,035276,035154,035030
	.WORD 034702,034552,034417,034262
	.WORD 034123,033762,033616,033450
	.WORD 033301,033127,032752,032574
	.WORD 032414,032232,032045,031657
	.WORD 031466,031274,031077,030701
	.WORD 030500,030276,030071,027663
	.WORD 027453,027241,027026,026610
	.WORD 026371,026150,025725,025501
	.WORD 025252,025022,024571,024336
	.WORD 024101,023643,023403,023141
	.WORD 022677,022432,022164,021715
	.WORD 021444,021172,020717,020442
	.WORD 020164,017705,017424,017143
	.WORD 016660,016374,016106,015620
	.WORD 015331,015040,014547,014254
	.WORD 013761,013464,013167,012671
	.WORD 012372,012072,011571,011270
	.WORD 010766,010463,010157,007653
	.WORD 007346,007041,006533,006225
	.WORD 005716,005406,005077,004566
	.WORD 004256,003745,003434,003122
	.WORD 002610,002276,001764,001452
	.WORD 001137,000625,000312,000000
MAPLST:	.WORD MAP1
	.WORD MAP2
	.WORD MAP3
	.WORD MAP4
	.WORD MAP5
MAP1:	.WORD 96.
	.WORD 96.
	.WORD 0.
	.BYTE 222,34,34,114,0,204,333,54
	.BYTE 36,136,136,153,20,364,5,355
	.BYTE 356,356,37,344,156,0,362,0
	.BYTE 336,162,0,33,137,176,276,0
	.BYTE 224,64,375,0,224,0,276,207
	.BYTE 77,224,0,261,0,13,275,37
	.BYTE 164,235,1,0,24,226,13,177
	.BYTE 33,324,201,166,204,37,355,304
	.BYTE 32,344,332,0,0,37,154,264
	.BYTE 1,166,204,337,270,24,40,77
	.BYTE 154,356,37,162,156,171,336,336
	.BYTE 74,136,136,20,77,54,137,166
	.BYTE 74,1,204,44,250,377
	.EVEN
MAP2:	.WORD 96.
	.WORD 96.
	.WORD 0.
	.BYTE 222,34,34,54,0,204,337,54
	.BYTE 37,60,60,45,66,102,1,26
	.BYTE 37,132,137,75,0,142,375,5
	.BYTE 164,0,144,144,354,13,162,37
	.BYTE 167,204,64,264,24,37,54,77
	.BYTE 13,1,66,44,37,271,224,41
	.BYTE 174,237,164,0,226,321,337,330
	.BYTE 122,1,101,62,261,77,73,60
	.BYTE 60,137,126,12,337,60,222,240
	.BYTE 377
	.EVEN
MAP3:	.WORD 96.
	.WORD 96.
	.WORD 0.
	.BYTE 222,32,34,52,0,204,362,52
	.BYTE 52,246,261,31,336,226,1,52
	.BYTE 335,77,60,354,5,100,77,57
	.BYTE 7,346,143,77,356,266,66,66
	.BYTE 136,326,37,333,77,0,136,120
	.BYTE 5,140,323,137,366,143,77,266
	.BYTE 306,137,326,37,323,137,160,173
	.BYTE 5,200,137,115,306,216,77,326
	.BYTE 333,137,37,133,137,115,220,354
	.BYTE 5,240,137,67,106,1,143,77
	.BYTE 344,66,66,66,20,126,237,12
	.BYTE 337,60,222,240,377
	.EVEN
MAP4:	.WORD 96.
	.WORD 96.
	.WORD 0.
	.BYTE 222,30,34,50,0,216,336,0
	.BYTE 116,50,300,37,63,76,332,276
	.BYTE 10,176,327,77,0,261,276,137
	.BYTE 154,37,35,116,336,124,36,216
	.BYTE 300,77,264,277,54,37,35,136
	.BYTE 266,16,316,156,10,265,236,77
	.BYTE 332,316,137,26,37,16,156,160
	.BYTE 166,100,256,237,6,336,320,277
	.BYTE 261,37,202,337,214,44,250,377
	.EVEN
MAP5:	.WORD 96.
	.WORD 96.
	.WORD 0.
	.BYTE 222,30,34,50,0,204,336,50
	.BYTE 37,112,357,0,23,132,167,0
	.BYTE 36,265,72,73,333,152,37,260
	.BYTE 170,1,261,37,333,330,73,260
	.BYTE 350,37,134,0,371,60,60,344
	.BYTE 0,3,265,152,137,330,72,37
	.BYTE 56,50,367,152,230,1,143,37
	.BYTE 151,1,144,37,26,121,10,155
	.BYTE 140,50,243,237,5,337,30,111
	.BYTE 120,377
	.EVEN
MTRLH:	.WORD 02.
	.WORD 04.
	.WORD 06.
	.WORD 08.
	.WORD 10.
	.WORD 12.
	.WORD 14.
	.WORD 16.
MTRLV:	.WORD 18.
	.WORD 20.
	.WORD 22.
	.WORD 24.
	.WORD 26.
	.WORD 28.
	.WORD 30.
	.WORD 32.
	.WORD ^B1000100010001000
	.WORD ^B0000000010001000
	.WORD ^B0100010010001000
	.WORD ^B0010001000010001
	.WORD ^B1000100001000100
	.WORD ^B0010001000110011
	.WORD ^B1100110010001000
	.WORD ^B0000000000110011
	.WORD ^B1100110000000000
	.WORD ^B0011001100110011
	.WORD ^B0100010010101010
	.WORD ^B1010101010011001
	.WORD ^B1001100101010101
	.WORD ^B0101010111111111
	.WORD ^B1111111101010101
	.WORD ^B0000000011111111
	.WORD ^B1111111100000000
	.WORD ^B1111111111111111
MTRLS:	.WORD ^B0000000000000000
	.WORD ^B1111111111111111
	.WORD ^B1111111100000000
	.WORD ^B0000000011111111
	.WORD ^B1111111110101010
	.WORD ^B1010101011111111
	.WORD ^B1010101010011001
	.WORD ^B0100010010101010
	.WORD ^B0101010101000100
	.WORD ^B1100110011001100
	.WORD ^B0011001100000000
	.WORD ^B0000000011001100
	.WORD ^B0011001100100010
	.WORD ^B1000100011001100
	.WORD ^B0010001000010001
	.WORD ^B0100010010001000
	.WORD ^B0001000100000000
	.WORD ^B0000000000100010
	.WORD ^B0010001000100010
MTRLM:	.WORD ^B1111111100000000
	.WORD ^B0000000011111111
	.WORD 0
	.WORD 0
	.WORD ^B1111111111111111
SCLR:	.WORD 4096.,2048.,1365.,1024.,819.,683.,585.,512.
	.WORD 455.,410.,372.,341.,315.,293.,273.,256.
	.WORD 241.,228.,216.,205.,195.,186.,178.,171.
	.WORD 164.,158.,152.,146.,141.,137.,132.,128.
BTT:	.WORD 1.,2.,4.,8.,16.,32.,64.,128.,256.
	.WORD 512.,1024.,2048.,4096.,8192.,16384.,-32768.
ANIMAS:	.WORD HNTRS,HITS,0,0,AMMOS
HNTRS:	.WORD 3.,HNTR1,HNTR2,HNTR3
HITS:	.WORD 3.,HIT1,HIT2,HIT3
AMMOS:	.WORD 10.,AMMO1,AMMO2,AMMO3,AMMO4,AMMO5,AMMO5
	.WORD AMMO4,AMMO3,AMMO2,AMMO1
HNTR1:	.WORD 0.,0.,-16.,32764.,16382.,-569.,31183.
	.WORD 14847.,-1537.,31175.,15823.,-2.
	.WORD 32764.,16368.,0.,0.
HNTR2:	.WORD 0.,0.,16368.,32764.,-2.,15823.,31175.
	.WORD -1537.,14847.,31183.,-569.,16382.
	.WORD 32764.,-16.,0.,0.
HNTR3:	.WORD 0.,0.,-16400.,-4.,32766.,-17977.
	.WORD -3897.,28927.,-20225.,-3897.,31175.
	.WORD -16386.,-4.,32752.,0.,0.
HIT1:	.WORD 0.,0.,0.,0.,96.,96.,96.,924.
	.WORD 924.,96.,96.,96.,0.,0.,0.,0.
HIT2:	.WORD 0.,0.,0.,96.,360.,264.,0.,1638.
	.WORD 1638.,0.,264.,360.,96.,0.,0.,0.
HIT3:	.WORD 0.,0.,516.,612.,0.,96.,96.,1530.
	.WORD 1530.,96.,96.,0.,612.,516.,0.,0.
AMMO1:	.WORD 0,0,0,7168.,32512.,30464.,-1664.,-640.
	.WORD -8320.,32512.,32512.,7168.,0,0,0,0
AMMO2:	.WORD 0,0,0,20032.,16256.,15232.,31936.,32448.
	.WORD 28608.,16256.,16256.,20032.,0,0,0,0
AMMO3:	.WORD 0,0,16400.,1792.,8128.,7616.,15968.
	.WORD 16224.,14304.,8128.,8128.,1792.,16400.,0,0,0
AMMO4:	.WORD 0,256.,8200.,896.,4064.,3808.,7984.
	.WORD 24500.,7152.,4064.,4064.,896.,8200.,256.,0,0
AMMO5:	.WORD 128.,8322.,4100.,448.,2032.,1904.
	.WORD 3992.,28635.,3576.,2032.,2032.,448.
	.WORD 4100.,8322.,128.,0
DRMSKH:	.WORD -32768.
	.WORD 16384.
	.WORD 8192.
	.WORD 4096.
	.WORD 2048.
	.WORD 1024.
	.WORD 512.
	.WORD 256.
	.WORD 128.
	.WORD 64.
	.WORD 32.
	.WORD 16.
	.WORD 8.
	.WORD 4.
	.WORD 2.
	.WORD 1.
DRMSKV:	.WORD -32768.
	.WORD -16384.
	.WORD -8192.
	.WORD -4096.
	.WORD -2048.
	.WORD -1024.
	.WORD -512.
	.WORD -256.
	.WORD -128.
	.WORD -64.
	.WORD -32.
	.WORD -16.
	.WORD -8.
	.WORD -4.
	.WORD -2.
	.WORD -1.
MSGPTR:	.WORD PMSG,WMSG,LMSG,EMSG
PMSG:	.WORD MSGLN+50
	.ASCIZ/Paused/
	.EVEN
WMSG:	.WORD MSGLN+40
	.ASCIZ/You Won!/
	.EVEN
LMSG:	.WORD MSGLN+34
	.ASCIZ/You Lost!/
	.EVEN
EMSG:	.WORD MSGLN+24+3220
	.ASCIZ/Press Enter/
	.EVEN

PREPM:	ASL R0
	MOV MAPLST(R0),R0
	MOV (R0)+,PLX
	MOV (R0)+,PLY
	MOV (R0)+,PLA
	MOV #MAP,R1
	CALL UNPK
	RET
CLVIS:	MOV #VIS,R5
	MOV #16.,R4
	MOV #-1.,R3
1$:	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	MOV R3,(R5)+
	SOB R4,1$
	RET
MLDVX:	XOR R2,R4
	XOR R4,R2
	XOR R2,R4
MLDVN:	ASL R2
	ASL R2
	ASL R2
	ASL R2
	ASL R2
	ASL R2
	CALL SPDIV
	RET
GETANG:	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV PLX,R0
	MOV PLY,R1
	MOV 4.(R4),R2
	MOV 6.(R4),R4
	SUB R0,R2
	BGE 1$
	SUB R1,R4
	BGE 2$
	NEG R2
	NEG R4
	CMP R2,R4
	BLE 3$
	CALL MLDVX
	BR 99$
3$:
	CALL MLDVN
	MOV R2,R0
	MOV #128.-1.,R2
	SUB R0,R2
	BR 99$
2$:	NEG R2
	CMP R2,R4
	BLE 4$
	CALL MLDVX
	MOV R2,R0
	MOV #64.+64.+384.-1.,R2
	SUB R0,R2
	BR 99$
4$:
	CALL MLDVN
	ADD #384.,R2
	BR 99$
1$:	SUB R1,R4
	BGE 5$
	NEG R4
	CMP R2,R4
	BLE 6$
	CALL MLDVX
	MOV R2,R0
	MOV #128.+64.+64.,R2
	SUB R0,R2
	BR 99$
6$:
	CALL MLDVN
	ADD #128.,R2
	BR 99$
5$:	CMP R2,R4
	BLE 7$
	CALL MLDVX
	ADD #256.,R2
	BR 99$
7$:
	CALL MLDVN
	MOV R2,R0
	MOV #256.+128.,R2
	SUB R0,R2
99$:	MOV (SP)+,R4
	MOV R2,8.(R4)
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	RET
CLPSC:	CLR R0
	MOV #PSCTAB,R3
	MOV #PSCTAB+64.,R5
1$:	MOV SCLR(R0),R1
	ADD #2,R0
	MOV R1,R2
	ASR R2
	MOV R0,-(SP)
	MOV R5,(R3)+
2$:	MOV R2,R4
	BIC #255.,R4
	SWAB R4
	ASL R4
	MOV BTT(R4),(R5)+
	ADD R1,R2
	SUB #2,(SP)
	BNE 2$
	TST (SP)+
	CMP #64.,R0
	BNE 1$
	RET
GSEG1:	ASL R0
	MOV SCTAB(R0),R1
	RET
GSEG2:	CALL GSEG4
	BR GSEGN
GSEG3:	CALL GSEG1
GSEGN:	NEG R1
	RET
GSEG4:	MOV #177,R1
	SUB R0,R1
	MOV R1,R0
	BR GSEG1
GCOS:	MOV R0,R1
	BIC #177600,R0
	BIC #177177,R1
	BEQ GSEG1
	CMP R1,#200
	BEQ GSEG2
	CMP R1,#400
	BEQ GSEG3
	BR GSEG4
GSIN:	MOV R0,R1
	BIC #177600,R0
	BIC #177177,R1
	BEQ GSEG4
	CMP R1,#200
	BEQ GSEG1
	CMP R1,#400
	BEQ GSEG2
	BR GSEG3
GSNCS:	CALL GSNCS2
	BR GSNCS3
GSNCS2:	MOV R0,-(SP)
	CALL GCOS
	MOV (SP)+,R0
	MOV R1,-(SP)
	CALL GSIN
	MOV (SP)+,R0
	SWAB R0
	MOVB R0,R0
	ASR R0
	SWAB R1
	MOVB R1,R1
	ASR R1
GSNCS3:	ASR R1
	ASR R0
	RET
SPMULS:	MOV R0,R2
	BPL 1$
	NEG R0
1$:	XOR R1,R2
	TST R1
	BPL 2$
	NEG R1
2$:	MOV R2,-(SP)
	CMP R0,R1
	BLOS 8$
	XOR R0,R1
	XOR R1,R0
	XOR R0,R1
8$:	CLR R2
	BR 6$
4$:	BCC 5$
	ADD R1,R2
5$:	ASL R1
6$:	ROR R0
	BNE 4$
	BCC 7$
	ADD R1,R2
7$:	TST (SP)+
	BPL 3$
	NEG R2
3$:	RET
SPMUL:	MOV R1,R2
	ROL R0
	SWAB R0
	TSTB R0
	BNE 1$
	CLR R2
	RET
1$:	ASL R1
	RORB R0
	BVC 2$
	ADD R1,R2
2$:	BNE 1$
	RET
SPDVX:	MOV #16384.,R2
SPDIV:	CLR R1
	CMP R4,R2
	BEQ 4$
	BHI 5$
	CLR R3
1$:	INC R3
	ASL R4
	BCS 3$
	CMP R2,R4
	BHIS 1$
	CLC
3$:	ROR R4
	CMP R4,R2
	BHI 2$
	SUB R4,R2
	SEC
	ROL R1
	SOB R3,3$
	MOV R1,R2
	RET
2$:	ASL R1
	SOB R3,3$
	MOV R1,R2
	RET
4$:	MOV #1,R2
	RET
5$:	CLR R2
	RET
CLRM:	CALL CLOPOS
	MOV #C_TADDR,R3
	CLR R0
	MOV #64.,R2
2$:	MOV #16.,R1
1$:	MOV R3,@#176640
	MOV R0,@#176642
	INC R3
	SOB R1,1$
	ADD #C_SCRWIH-16.,R3
	SOB R2,2$
	RET
DRAWM:	CALL CLOPOS
	MOV #MAP,R2
	MOV #C_TADDR,R3
	MOV #176640,R5
	MOV #176642,R0
	MOV #16.,-(SP)
3$:	MOV #16.,R1
2$:	CLR R4
	TSTB (R2)+
	BEQ 1$
	MOV #377,R4
1$:	MOV R3,(R5)
	MOV R4,(R0)
	ADD #C_SCRWIH,R3
	MOV R3,(R5)
	MOV R4,(R0)
	ADD #C_SCRWIH,R3
	MOV R3,(R5)
	MOV R4,(R0)
	ADD #C_SCRWIH,R3
	MOV R3,(R5)
	MOV R4,(R0)
	SUB #C_SCRWIH+C_SCRWIH+C_SCRWIH-1.,R3
	SOB R1,2$
	ADD #C_SCRWIH+C_SCRWIH+C_SCRWIH+C_SCRWIH-16.,R3
	DEC (SP)
	BNE 3$
	TST (SP)+
	RET
PLPNT:	ASR R1
	ASR R1
	ASR R1
	MOV R1,R2
	BIC #177771,R2
	ASR R1
	ASR R1
	ASR R1
	ASR R3
	ASR R3
	ASR R3
	ASR R3
	MUL #C_SCRWIH,R3
	ADD R1,R3
	ADD #C_TADDR,R3
	MOV R3,@#176640
	MOV @#176642,R3
	BIC CLMSK(R2),R3
	DEC R0
	BNE 1$
4$:	MOV R3,@#176642
	RET
1$:	DEC R0
	BNE 2$
	BIS DRMS1(R2),R3
	BR 4$
2$:	DEC R0
	BNE 3$
	BIS DRMS2(R2),R3
	BR 4$
3$:	DEC R0
	BNE 4$
	BIS DRMS3(R2),R3
	BR 4$
MNDRW:	MOV #OPOS,R4
	MOV #SMNBF,R5
2$:	MOV (R4),R1
	BEQ 5$
	MOV 2(R4),R3
	MOV #1.,R0
	CALL PLPNT
5$:	TSTB (R5)
	BEQ 3$
	MOV 4.(R5),R1
	MOV R1,(R4)+
	MOV 6.(R5),R3
	MOV R3,(R4)+
	MOV #3.,R0
	CALL PLPNT
	BR 4$
3$:	CLR (R4)+
	CLR (R4)+
4$:	ADD #MNRS,R5
	CMP #SMNBFE,R5
	BNE 2$
	MOV (R4),R1
	BEQ 6$
	MOV 2(R4),R3
	MOV #1.,R0
	CALL PLPNT
6$:	MOV SPLX,R1
	MOV SPLY,R3
	MOV R1,(R4)+
	MOV R3,(R4)+
	MOV #2,R0
	CALL PLPNT
	RET
CLOPOS:	MOV #OPOS,R5
	CLR (R5)+
	CMP R5,#OPOS+124.
	BNE .-6
	RET
OPOS:	.BLKW 62.
CHECKP:	MOV R0,R3
	MOV R1,R2
	ASL R3
	ASL R3
	SWAB R3
	ASR R2
	ASR R2
	BIC #15.,R2
	BISB R3,R2
	RET
CHECK:	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV #DIRS,R4
1$:	ADD (R4)+,R0
	ADD (R4)+,R1
	CALL CHECKP
	TSTB MAP(R2)
	BNE 2$
	CMP R4,#DIRS+32.
	BNE 1$
	CLR R4
2$:	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	TST R4
	RET
DIRS:	.WORD -8.,-8.,8.,0.,8.,0.,0.,8.
	.WORD 0.,8.,-8.,0.,-8.,0.,0.,-8.
FRN:	.WORD 327
RAN:	.WORD 0
	INCB RAN
	ROLB FRN+3
MM:	ADD #0,RAN
	ADD #3337,MM+2
	RET
PRCH:	MOV R0,PPUV1
	MOV R1,PPUV2
	MOV #F_PMSG,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	MOV PPUV2,R1
	RET
DOUT:	CLR R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	SUB #10.,R4
	BCS 1$
	INC R5
	BR 2$
1$:	ADD #10.,R4
2$:	MOV R5,R0
	ADD #'0,R0
	CALL PRCH
	MOV R4,R0
	ADD #'0,R0
	CALL PRCH
	RET
INISCR:	CLR SCORE
	MOV #1.,OLDSCR
	RET
PRNSCR:	CMP SCORE,OLDSCR
	BEQ 1$
	MOV #SCOREP,R1
	MOVB SCORE,R4
	CALL DOUT
	MOVB #':,R0
	CALL PRCH
	MOVB SCORE+1,R4
	CALL DOUT
	MOV SCORE,OLDSCR
1$:	RET
INIAM:	MOVB #10.,AMMO
	MOVB #1.,OLDAM
	RET
PRNAM:	CMPB AMMO,OLDAM
	BEQ 1$
	MOV #AMMOP,R1
	MOVB AMMO,R4
	CALL DOUT
	MOVB AMMO,OLDAM
1$:	RET
PRNMS:	ASL R5
	MOV MSGPTR(R5),R5
	MOV (R5)+,R1
2$:	MOVB (R5)+,R0
	BEQ 1$
	CALL PRCH
	BR 2$
1$:	RET
CLMS:	MOV #MSGLN+20,R5
	MOV #40.,R3
2$:	MOV #60,R4
1$:	MOV R5,@#176640
	INC R5
	CLR @#176642
	SOB R4,1$
	ADD #C_SCRWIH-60,R5
	SOB R3,2$
	RET
CORRA:	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	CLR (PC)+
STEPN:	.WORD 0
	MOV MNBF+8.,R5
	SUB PLA,R5
	MOV #STEPC,R4
	MOV #10.,R3
1$:	MOV R5,R0
	MOV (R4)+,R1
	CALL SPMULS
	ASR R2
	ASR R2
	ASR R2
	ASR R2
	ASR R2
	ASR R2
	ADD PLA,R2
	BIC #177000,R2
	MOV R2,20.-2.(R4)
	SOB R3,1$
	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	RET
STEPC:	.WORD 2.,6.,13.,22.,32.,42.,51.,58.,62.,64.
STEPV:	.BLKW 10.
SETTA:	MOV STEPN,R0
	CMP R0,#20.
	BEQ 1$
	ADD #2,R0
	MOV R0,STEPN
	MOV STEPV-2(R0),PLA
1$:	RET
PSCTAB:	.BLKB 1120.

PREP0:	MOV #UR0,R5
	MOV #64.,R4
	MOV #C_VADDR-FRBF3-2+50000-500,R3
10$:	MOV #010063,(R5)+
	MOV R3,(R5)+
	MOV R3,R1
	ADD #C_SCRWID,R1
	MOV #010163,(R5)+
	MOV R1,(R5)+
	SUB #C_SCRWID*2,R3
	SOB R4,10$
	MOV #207,(R5)
	MOV #DD0,R5
	MOV #64.,R4
	MOV #UR0+504.,R3
20$:	MOV R3,(R5)+
	SUB #8.,R3
	SOB R4,20$
	RET
PREPR:	MOV #FRBF1,R0
	MOV #256.,R1
1$:	MOV #512.,(R0)+
	SOB R1,1$
	MOV #FRBF3,R0
	MOV #64.,R1
2$:	MOV #64.,(R0)+
	SOB R1,2$
	RET
DRSP0:	MOV R2,FRBF1(R5)
	MOV R0,FRBF2(R5)
	RET
DRSP2:	MOV R2,FRBF1+2(R5)
	MOV R0,FRBF2+2(R5)
	RET
DRSP4:	MOV R2,FRBF1+4(R5)
	MOV R0,FRBF2+4(R5)
	RET
DRSP6:	MOV R2,FRBF1+6(R5)
	MOV R0,FRBF2+6(R5)
	RET
DRDSP0:	MOV R2,FRBF1(R5)
	MOV R0,FRBF2(R5)
	MOV R1,FRBF1+2(R5)
	MOV R0,FRBF2+2(R5)
	RET
DRDSP4:	MOV R2,FRBF1+4(R5)
	MOV R0,FRBF2+4(R5)
	MOV R1,FRBF1+2+4(R5)
	MOV R0,FRBF2+2+4(R5)
	RET
DRDIFF:	MOV #64.,R3
	MOV #FRBF3,R5

50$:	CMP R5,#FRBF4
	BLO 10$
	RET

10$:	MOV (R5)+,R1
	MOV -256.-2.(R5),R2
	ASR R2
	ASR R2
	ASR R2
	BEQ 50$
	CMP R3,R2
	BHIS 30$
	MOV R3,R2
30$:	CMP R1,R2
	BEQ 70$
	BLO 40$

	MOV R2,-2.(R5)
	CALL IDRF
	MOV -2.(R5),R2

70$:	MOV -128.-2.(R5),R0
	CMP 128.-2.(R5),R0
	BEQ 50$

20$:	MOV R0,128.-2.(R5)
	MOV R0,R1
	NEG R1

	SUB R2,R3

	BIT #1,R3
	BEQ 22$
	NEG R0
	NEG R1
22$:

	MOV MTRLS(R0),R0
	MOV MTRLS(R1),R1

	MUL #C_SCRWID,R3
	ADD R5,R3

	ASL R2
	CALL @DD0-2(R2)

	MOV #64.,R3
	BR 50$

40$:	MOV R2,-2.(R5)
	MOV -128.-2.(R5),R0
	CMP 128.-2.(R5),R0
	BNE 20$

	MOV R0,R4
	CALL IDRW

	BR 50$
IDRF:	XOR R1,R2
	XOR R2,R1
	XOR R1,R2
	MOV #34.,R0
	MOV #36.,R4
IDRW:	SUB R2,R3

	BIT #1,R3
	BEQ 22$
	NEG R0
	NEG R4
22$:

	MUL #C_SCRWID,R3
	ADD R5,R3
	ADD #C_VADDR-FRBF3-2,R3

	SUB R1,R2
	ASL R1
	ADD R2,R1

	BIT #1,R1
	BEQ 24$
	NEG R4
24$:

	MUL #C_SCRWID,R1
	MOV R1,ID4+4

	MOV #C_SCRWID,R1
ID3:	MOV MTRLS(R0),(R3)
ID4:	MOV MTRLS(R4),000000(R3)
	NEG R0
	NEG R4
	ADD R1,R3
	SOB R2,ID3

	MOV #64.,R3
	RET
CORE:	MOV R5,-(SP)
	MOV #64.,-(SP)
	MOV #DA,R5
	MOV (R5)+,R3
	BLE S1
	MOV #5201,DDII
	MOV (SP),R1
	SUB DU0,R1
	BR S2
S1:	MOV #5301,DDII
	MOV DU0,R1
	INC R1
	NEG R3
S2:	MOV (R5)+,R0
	BLE S3
	MOV #62701,DDJI
	MOV (SP),R4
	SUB DV0,R4
	BR S4
S3:	MOV #162701,DDJI
	MOV DV0,R4
	INC R4
	NEG R0
S4:	MOV R1,(R5)+
	MOV R4,(R5)
	MOV R0,R5
	CALL SPMUL
	MOV R3,R0
	MOV R4,R1
	MOV R2,R4
	CALL SPMUL
	MOV DIJ0,R1
S5:	MOVB FRCNTR,256.(R1)
	CMP R4,R2
	BLT S6
	ADD (SP),DDY
	SUB R2,R4
	MOV R3,R2
DDJI:	ADD #16.,R1
	MOVB (R1),R0
	BEQ S5
	MOV R5,R2
	MOV DDY,R4
	SUB (SP)+,R4
	CALL SPDIV
	MOVB R0,R4
	BIC #177761,R0
	MOV MTRLH(R0),R0
	MOV (SP)+,R5
	RET
S6:	ADD (SP),DDX
	SUB R4,R2
	MOV R5,R4
DDII:	INC R1
	MOVB (R1),R0
	BEQ S5
	MOV R3,R2
	MOV DDX,R4
	SUB (SP)+,R4
	CALL SPDIV
	MOVB R0,R4
	COMB R4
	BIC #177761,R0
	MOV MTRLV(R0),R0
	MOV (SP)+,R5
	RET
RENDR:	MOV #DU0,R5
	MOV SPLX,R0
	MOV R0,(R5)
	BIC #177700,(R5)+
	MOV SPLY,R1
	MOV R1,(R5)
	BIC #177700,(R5)+
	BIC #77,R1
	ASR R0
	ASR R0
	ASR R0
	ASR R0
	ADD R1,R0
	ASR R0
	ASR R0
	ADD #MAP,R0
	MOV R0,(R5)+
	MOV SPLA,R0
	CALL GCOS
	MOV R1,(R5)+
	NEG R1
	MOV R1,(R5)+
	MOV R1,R2
	MOV SPLA,R0
	CALL GSIN
	MOV R1,(R5)+
	MOV R1,(R5)+
	ASR R1
	ASR R1
	ASR R1
	MOV R1,(R5)+
	ASR R1
	MOV R1,(R5)+
	ASR R1
	MOV R1,(R5)+
	ASR R2
	ASR R2
	ASR R2
	MOV R2,(R5)+
	ASR R2
	MOV R2,(R5)+
	ASR R2
	MOV R2,(R5)+
	MOV DVX,R1
	MOV DNX,R2
	SUB R2,R1
	MOV R1,(R5)+
	MOV DVY,R1
	MOV DNY,R2
	SUB R2,R1
	MOV R1,(R5)
	MOV #128.-8.,R5
	CALL CORE
	MOV R2,(PC)+
PREVS:	.WORD 0
	MOV R4,(PC)+
PREVT:	.WORD 0
RLP:	ADD DDA,DA
	ADD DDB,DB
	CALL CORE
	CMP PREVT,R4
	BNE NSM
	MOV PREVS,R1
	MOV R2,PREVS
	MOV R1,R3
	ADD R2,R1
	ASR R1
	MOV R1,R4
	ADD R2,R1
	ASR R1
	CALL DRDSP0
	MOV R4,R2
	MOV R3,R1
	ADD R2,R1
	ASR R1
	CALL DRDSP4
NXR:	SUB #8.,R5
	BGE RLP
	RET
NSM:
	MOV DA,-(SP)
	MOV DB,-(SP)
	MOV R0,-(SP)
	MOV PREVT,-(SP)
	MOV PREVS,-(SP)
	MOV R2,PREVS
	MOV R4,PREVT
	SUB DDA2,DA
	SUB DDB2,DB
	CALL CORE
	MOV (SP)+,R1
	CMP (SP)+,R4
	BNE NT1
	ADD R2,R1
	ASR R1
	CALL DRDSP4
	BR NT2
NT1:	MOV R2,-(SP)
	MOV R4,-(SP)
	CALL DRSP4
	SUB DDA4,DA
	SUB DDB4,DB
	CALL CORE
	CALL DRSP6
	MOV (SP)+,R4
	MOV (SP)+,R2
NT2:	MOV (SP)+,R0
	CMP PREVT,R4
	BNE NT3
	MOV R2,R1
	MOV PREVS,R2
	ADD R2,R1
	ASR R1
	CALL DRDSP0
	BR NT4
NT3:
	MOV PREVS,R2
	CALL DRSP0
	MOV (SP),DB
	MOV 2(SP),DA
	SUB DDA4,DA
	SUB DDB4,DB
	CALL CORE
	CALL DRSP2
NT4:	MOV (SP)+,DB
	MOV (SP)+,DA
	BR NXR
DU0:	.WORD 0
DV0:	.WORD 0
DIJ0:	.WORD 0
DVX:	.WORD 0
DNY:	.WORD 0
DVY:	.WORD 0
DNX:	.WORD 0
DDA:	.WORD 0
DDA2:	.WORD 0
DDA4:	.WORD 0
DDB:	.WORD 0
DDB2:	.WORD 0
DDB4:	.WORD 0
DA:	.WORD 0
DB:	.WORD 0
DDX:	.WORD 0
DDY:	.WORD 0
MAP:	.BLKW 200
VIS:	.BLKW 200
FRBF1:	.BLKW 100
FRBF2:	.BLKW 100
FRBF3:	.BLKW 100
FRBF4:	.BLKW 100
DD0:	.BLKW 64.
UR0:	.BLKW 257.

WMS:	.WORD 0
CLMM:	MOV WMS,R3
	ASL R0
0$:	BCS 1$
	BEQ 2$
4$:	ADD #C_SCRWID+C_SCRWID,R3
	ASL R0
	BR 0$
1$:	MOV R1,R2
	MOV R3,R4
	ASL R2
5$:	BCS 3$
	BEQ 4$
	ADD #8.,R4
	ASL R2
	BR 5$
3$:	CLR C_SCRWID(R4)
	CLR (R4)+
	CLR C_SCRWID(R4)
	CLR (R4)+
	CLR C_SCRWID(R4)
	CLR (R4)+
	CLR C_SCRWID(R4)
	CLR (R4)+
	ASL R2
	BR 5$
2$:	RET
MSKSIN:	MOV #-1.,(PC)+
MHMSK:	.WORD 0
	MOV #-1.,(PC)+
MVMSK:	.WORD 0
	MOV #-1.,(PC)+
XHMSK:	.WORD 0
	MOV #-1.,(PC)+
XVMSK:	.WORD 0
	RET
MNPRC:	BITB FRCNTR,#2
	BEQ 1$
	MOV #C_MADDR,WMS
	MOV MVMSK,R0
	MOV MHMSK,R1
	BR 2$
1$:	MOV #C_XADDR,WMS
	MOV XVMSK,R0
	MOV XHMSK,R1
2$:     CALL CLMM
	CLR (PC)+
HMSK:	.WORD 0
	CLR (PC)+
VMSK:	.WORD 0
	CALL CLCD
	CALL SORT
	CALL CLDP
	BITB FRCNTR,#2
	BEQ 1$
	MOV VMSK,MVMSK
	MOV HMSK,MHMSK
	MOV #V_SC0,R0
	BR 2$
1$:	MOV VMSK,XVMSK
	MOV HMSK,XHMSK
	MOV #V_SC2,R0
2$:	CALL SCH2
	RET
CLCD:   MOV SPLA,R0
	CALL GCOS
	SWAB R1
	MOVB R1,R1
	MOV R1,(PC)+
XCOS:	.WORD 0
	MOV SPLA,R0
	CALL GSIN
	SWAB R1
	MOVB R1,R1
	MOV R1,(PC)+
XSIN:	.WORD 0

	CLR SORTN

	MOV #SMNBF,R5

1$:	TSTB (R5)
	BEQ 2$
	CLR R0
	BISB 3.(R5),R0
	CMPB VIS(R0),FRCNTR
	BNE 2$

	MOV 4.(R5),R0
	SUB SPLX,R0
	ASR R0
	MOV XCOS,R1
	CALL SPMULS

	MOV 6.(R5),R0
	SUB SPLY,R0
	ASR R0
	MOV XSIN,R1
	MOV R2,R4
	CALL SPMULS
	ADD R4,R2

	ASR R2
	ASR R2
	ASR R2
	ASR R2
	ASR R2

	MOV R2,10.(R5)
	BLE 2$

	MOV SORTN,R2
	ASL R2
	MOV R5,SORTI(R2)
	INC SORTN

2$:	ADD #MNRS,R5
	CMP #SMNBFE,R5
	BNE 1$
	RET
SORT:	MOV #1,R0
2$:	CMP R0,SORTN
	BGE 1$
	MOV SORTN,R1
	SUB R0,R1
	CLR R2
4$:	CMP R2,R1
	BGE 3$
	MOV R2,R3
	ASL R3
	MOV SORTI(R3),R4
	MOV SORTI+2(R3),R5
	CMP 10.(R5),10.(R4)
	BGE 5$
	MOV R4,SORTI+2(R3)
	MOV R5,SORTI(R3)
5$:	INC R2
	BR 4$
3$:	INC R0
	BR 2$
1$:	RET
SORTN:	.WORD 0
SORTI:	.BLKW 30.
CLDP:	MOV SORTN,R5

1$:	BEQ 6$
	DEC R5
	MOV R5,-(SP)
	ASL R5
	MOV SORTI(R5),R5

	MOV 10.(R5),R4

	MOV 4.(R5),R0
	SUB SPLX,R0
	ASR R0
	MOV XSIN,R1
	CALL SPMULS

	MOV 6.(R5),R0
	SUB SPLY,R0
	ASR R0
	MOV XCOS,R1
	MOV R2,R3
	CALL SPMULS

	SUB R3,R2
	BPL 3$
	NEG R2
	CALL SPDIV
	NEG R2
	BR 4$
3$:	CALL SPDIV

4$:	MOV R2,R0
	BPL 5$
	NEG R2
5$:	CMP R2,#64.
	BHIS 2$
	ADD #32.,R0

	MOVB (R5),R3
	ASL R3
	MOV DRTAB-2(R3),R3

	CALL (R3)

2$:	MOV (SP)+,R5
	BR 1$

6$:	RET
DRTAB:	.WORD SPDRW
	.WORD SPDRW
	.WORD SPDRF
	.WORD SPDRF
	.WORD SPDRW
XFRAMS:	.WORD XFR1,XFR2,XFR3,XFR4,XFR5,XFR6
XSCLRS:	.BYTE 1,1,1,1,1,1,1,1
	.BYTE 2,2,2,2
	.BYTE 3,3,3,3,3
	.BYTE 4,4,4,4,4
	.BYTE 5,5,5,5,5
	.BYTE 6,6,6,6,6
XSIZES:	.BYTE 1,3,3,5,5,7
XFR1:	.BYTE 377
XFR2:	.BYTE 0,377,0
	.BYTE 377,377,377
	.BYTE 0,377,0
XFR3:	.BYTE 377,377,377
	.BYTE 377,377,377
	.BYTE 377,377,377
XFR4:	.BYTE 0,0,377,0,0
	.BYTE 0,377,377,377,0
	.BYTE 377,377,377,377,377
	.BYTE 0,377,377,377,0
	.BYTE 0,0,377,0,0
XFR5:	.BYTE 0,377,377,377,0
	.BYTE 377,377,377,377,377
	.BYTE 377,377,377,377,377
	.BYTE 377,377,377,377,377
	.BYTE 0,377,377,377,0
XFR6:	.BYTE 0,0,377,377,377,0,0
	.BYTE 0,377,377,377,377,377,0
	.BYTE 377,377,377,377,377,377,377
	.BYTE 377,377,377,377,377,377,377
	.BYTE 377,377,377,377,377,377,377
	.BYTE 0,377,377,377,377,377,0
	.BYTE 0,0,377,377,377,0,0
	.EVEN
SCTB:	.WORD SCL1,SCL2,SCL3,SCL4,SCL5,SCL6,SCL5,SCL6
	.WORD SCL5,SCL6,SCL5,SCL6,SCL5,SCL6,SCL5,SCL6
	.WORD SCL5,SCL6,SCL5,SCL6,SCL5,SCL6,SCL5,SCL6
	.WORD SCL5,SCL6,SCL5,SCL6,SCL5,SCL6,SCL5,SCL6
SPDRF:	MOV R5,-(SP)
	MOV 10.(R5),R4
	CALL SPDVX
	MOV R2,-(SP)
	ASR R2
	ASR R2
	ASR R2
	ASR R2
	BEQ XR1
	CMP R2,#32.
	BHI XR1
	MOV R2,R3
	ASR R3
	MOVB XSCLRS-1(R2),R2
	MOV R2,R5
	ASL R5
	MOV XFRAMS-2(R5),R4
	MOVB XSIZES-1(R2),R2
	MOV R2,R5
	ASR R5
	SUB R5,R0
	MOV R2,R1
	SUB R5,R3
	MOV R2,R5
	ADD R3,R5
	MOV DRMSKV-1(R5),(PC)+
VTMP:	.WORD 0
	MUL #C_SCRWID,R3
XR2:	CMPB R0,#64.
	BHIS XR4
	MOVB R0,R5
	ASL R5
	CMP FRBF1(R5),(SP)
	BGE XR4
	MOVB R0,R5
	ASR R5
	BIS DRMSKH(R5),HMSK
	BIS VTMP,VMSK
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	ASL R0
	ADD R3,R0
	ADD WMS,R0
XR3:	MOVB (R4)+,R1
	BEQ XR5
	MOV R1,(R0)
XR5:	ADD #C_SCRWID,R0
	SOB R2,XR3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
XR4:	INC R0
	SOB R1,XR2
XR1:	TST (SP)+
	MOV (SP)+,R5
	RET
SPDRW:	MOV R5,-(SP)
	CALL DPROJ2
	MOV 10.(R5),R4
	CALL SPDVX
	MOV R2,-(SP)
	ASR R2
	ASR R2
	ASR R2
	ASR R2
	BEQ SR1
	CMP R2,#32.
	BHI SR1
	MOVB (R5),R4
	ASL R4
	MOV ANIMAS-2(R4),R4
	BEQ TSCLX
	MOVB 1.(R5),R1
	INC R1
	ASL R1
	ADD R1,R4
	MOV (R4),(PC)+
ANIFR:	.WORD 0
TSCLX:	MOV R2,R4
	ASL R4
	MOV SCTB-2.(R4),SR0+2
	MOV SCLR-2.(R4),(PC)+
TSCLR:	.WORD 0
	MOV R2,R4
	ASR R4
	SUB R4,R0
	MOV R2,R1
	DEC R1
	MOV R1,-(SP)
	MOVB (R5),R1
	ASL R1
	MOV MTRLM-2.(R1),R1
	MOV TSCLR,(PC)+
HSCLR:	.WORD 0
	ASR HSCLR
SR2:	CMPB R0,#64.
	BHIS SR4
	MOVB R0,R5
	ASL R5
	CMP FRBF1(R5),2(SP)
	BGE SR4
	MOVB HSCLR+1,R4
	ASL R4
	ADD ANIFR,R4
	MOV (R4),R4
	BEQ SR4
	MOVB R0,R5
	ASR R5
	BIS DRMSKH(R5),HMSK
	MOV R2,R5
	BIS DRMSKV-1.(R5),VMSK
	ASL R5
	MOV PSCTAB-2.(R5),R3
	MOV R2,-(SP)
	MOV R0,R5
	ASL R5
	ADD WMS,R5
SR0:	MOV #0,PC
SR5:	MOV (SP)+,R2
SR4:	INC R0
	ADD TSCLR,HSCLR
	DEC (SP)
	BPL SR2
	TST (SP)+
SR1:	TST (SP)+
	MOV (SP)+,R5
	RET
SCL1:	BIT (R3)+,R4
	BEQ SR5
	MOV R1,(R5)
1$:	BR SR5
SCL2:	BIT (R3)+,R4
	BEQ 1$
	MOV R1,(R5)
1$:	BIT (R3)+,R4
	BEQ 2$
	MOV R1,C_SCRWID(R5)
2$:	BR SR5
SCL3:	BIT (R3)+,R4
	BEQ 1$
	MOV R1,(R5)
1$:	BIT (R3)+,R4
	BEQ 2$
	MOV R1,C_SCRWID(R5)
2$:	BIT (R3)+,R4
	BEQ 3$
	MOV R1,C_SCRWID+C_SCRWID(R5)
3$:	BR SR5
SCL4:	BIT (R3)+,R4
	BEQ 1$
	MOV R1,(R5)
1$:	BIT (R3)+,R4
	BEQ 2$
	MOV R1,C_SCRWID(R5)
2$:	BIT (R3)+,R4
	BEQ 3$
	MOV R1,C_SCRWID+C_SCRWID(R5)
3$:	BIT (R3)+,R4
	BEQ 4$
	MOV R1,C_SCRWID+C_SCRWID+C_SCRWID(R5)
4$:	BR SR5
SCL5:	ASR R2
	BIT (R3)+,R4
	BEQ 2$
	MOV R1,(R5)
2$:	ADD #C_SCRWID,R5
1$:	BIT (R3)+,R4
	BEQ 3$
	MOV R1,(R5)
3$:	BIT (R3)+,R4
	BEQ 4$
	MOV R1,C_SCRWID(R5)
4$:	ADD #C_SCRWID+C_SCRWID,R5
	SOB R2,1$
	BR SR5
SCL6:	ASR R2
1$:	BIT (R3)+,R4
	BEQ 3$
	MOV R1,(R5)
3$:	BIT (R3)+,R4
	BEQ 4$
	MOV R1,C_SCRWID(R5)
4$:	ADD #C_SCRWID+C_SCRWID,R5
	SOB R2,1$
	BR SR5

MNPRCS:	.WORD MHNTR,MHIT,MPROJ1,MPROJ2,MAMMO
CLMNBF:	MOV #MNBF,R5
1$:	CLR (R5)+
	CMP #MNBFE,R5
	BNE 1$
	RET
SETIPS:	MOV R3,-(SP)
1$:	CALL FRN
	MOV RAN,R0
	BIC #176000,R0
	CALL FRN
	MOV RAN,R1
	BIC #176000,R1
	CALL CHECK
	BNE 1$
	CALL FRN
	MOV RAN,R2
	BIC #177000,R2
	MOV R2,8.(R5)
	MOV R0,4.(R5)
	MOV R1,6.(R5)
	CALL CHECKP
	MOVB R2,3.(R5)
	CLR R3
	BISB FRCNTR,R3
	CMPB VIS(R2),R3
	BEQ 1$
	DECB R3
	DECB R3
	CMPB VIS(R2),R3
	BEQ 1$
	MOV R0,-(SP)
	CALL FRN
	MOV RAN,R0
	BIC #177760,R0
	CMPB R0,2.(R5)
	BLO 2$
	CLR R0
2$:	MOVB R0,1.(R5)
	MOV (SP)+,R0
	MOV (SP)+,R3
	RET
SETHNT:	MOV #MNBF,R5
1$:	MOVB #1.,(R5)
	CLRB 1.(R5)
	MOV HNTRS,R0
	MOVB R0,2.(R5)
	CALL SETIPS
	MOVB #255.,MAP(R2)
	ADD #MNRS,R5
	CMP #MNBFS,R5
	BNE 1$
	RET
SETAMM:	MOV #MNBFX,R5
1$:	MOVB #5.,(R5)
	CLRB 1.(R5)
	MOV AMMOS,R0
	MOVB R0,2.(R5)
	CALL SETIPS
	MOVB #255.,MAP(R2)
	ADD #MNRS,R5
	CMP #MNBFE,R5
	BNE 1$
	RET
CLTMRK:	MOV #MAP,R5
	MOV #256.,R4
1$:	CMPB (R5),#255.
	BNE 2$
	CLRB (R5)
2$:	INC R5
	SOB R4,1$
	RET
MNSET:	CALL MSKSIN
	CALL CLMNBF
	CALL SETHNT
	CALL SETAMM
	CALL CLTMRK
	RET
NEXTFR:	BIT SKIP,#3.
	BNE 1$
	INCB 1.(R5)
	CMPB 1.(R5),2.(R5)
	BNE 1$
	CLRB 1.(R5)
1$:	RET
MHNTR:	MOV 8.(R5),R0
	CALL GSNCS
	ASR R0
	ASR R1
	ADD 4.(R5),R0
	ADD 6.(R5),R1
	CALL CHECK
	BNE 1$
	MOV R0,4.(R5)
	MOV R1,6.(R5)
	CALL CHECKP
	MOVB R2,3.(R5)
	BR 2$
1$:	CALL FRN
	MOV RAN,R2
	BIC #177000,R2
	MOV R2,8.(R5)
	CMP GSTATE,#GSHTWN
	BNE 2$
	CMP R5,#MNBF
	BNE 2$
	CALL CORRA
2$:	CALL NEXTFR
	RET
MHIT:	CALL NEXTFR
	BNE 1$
	CLRB (R5)
1$:	RET
HPCHK:	CLR R2
	CMP GSTATE,#GSGAME
	BNE 5$
	MOV R0,-(SP)
	MOV R1,-(SP)
	SUB PLX,R0
	BPL 1$
	NEG R0
1$:	SUB PLY,R1
	BPL 2$
	NEG R1
2$:	CMP R0,#HITRAD
	BHI 4$
	CMP R1,#HITRAD
	BHI 4$
	MOVB SCORE,R0
	INCB R0
	CMPB R0,#SCLIM
	BLO 3$
	MOV #GSHTWN,GSTATE
	MOV R0,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R5,-(SP)
	BIS #16.,BITFLD
	CALL CORRA
	MOV (SP)+,R5
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R0
3$:	MOVB R0,SCORE
	INC R2
4$:	MOV (SP)+,R1
	MOV (SP)+,R0
5$:	RET
DALL:	MOV #MNBF,R5
1$:	TSTB (R5)
	BEQ 2$
	MOVB #2.,(R5)
	CLRB 1.(R5)
	MOV HITS,R0
	MOVB R0,2.(R5)
	MOV #H_SND,R0
	CALL SCH2
2$:	ADD #MNRS,R5
	CMP #MNBFS,R5
	BNE 1$
	RET
PHCHK:	CLR R2
	CMP GSTATE,#GSGAME
	BNE 7$
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R5,-(SP)
	MOV #MNBF,R5
1$:	TSTB (R5)
	BEQ 2$
	MOV 4(SP),R0
	MOV 2(SP),R1
	SUB 4(R5),R0
	BPL 3$
	NEG R0
3$:	SUB 6(R5),R1
	BPL 4$
	NEG R1
4$:	CMP R0,#HITRAD
	BHI 2$
	CMP R1,#HITRAD
	BHI 2$
	MOVB SCORE+1,R0
	INCB R0
	CMPB R0,#SCLIM
	BLO 5$
	MOV #GSPLWN,GSTATE
	MOV R0,-(SP)
	MOV R3,-(SP)
	BIS #32.,BITFLD
	CALL DALL
	MOV (SP)+,R3
	MOV (SP)+,R0
	CLR R5
5$:	MOVB R0,SCORE+1
	MOV R5,R2
	BR 6$
2$:	ADD #MNRS,R5
	CMP #MNBFS,R5
	BNE 1$
6$:	MOV (SP)+,R5
	MOV (SP)+,R1
	MOV (SP)+,R0
7$:	RET
MPROJ1:	MOV 8.(R5),R0
	CALL GSNCS2
	ADD 4.(R5),R0
	ADD 6.(R5),R1
	CMPB (R5),#4.
	BNE 4$
	CALL HPCHK
	TST R2
	BEQ 5$
	MOV 8.(R5),R0
	CALL GSNCS
	CALL DUP2
	BR 1$
4$:	CALL PHCHK
	TST R2
	BEQ 5$
	MOV 4.(R2),4.(R5)
	MOV 6.(R2),6.(R5)
	MOV R5,-(SP)
	MOV R2,R5
	CALL SETIPS
	MOV (SP)+,R5
	BR 1$
5$:	CALL CHECK
	BNE 1$
	MOV R0,4.(R5)
	MOV R1,6.(R5)
	CALL CHECKP
	MOVB R2,3.(R5)
	BR 2$
1$:	MOVB #2.,(R5)
	CLRB 1.(R5)
	MOV HITS,R0
	MOVB R0,2.(R5)
	MOV #H_SND,R0
	CALL SCH2
	BR 3$
2$:	CALL NEXTFR
3$:	RET
MPROJ2:	JMP MPROJ1
MAMMO:	CMPB AMMO,#30.
	BHIS 3$
	MOV PLX,R0
	MOV PLY,R1
	SUB 4.(R5),R0
	BPL 1$
	NEG R0
1$:	SUB 6.(R5),R1
	BPL 2$
	NEG R1
2$:	CMP R0,#HITRAD
	BHI 3$
	CMP R1,#HITRAD
	BHI 3$
	MOVB AMMO,R0
	ADD #5.,R0
	CMP R0,#30.
	BLOS 4$
	MOV #30.,R0
4$:	MOVB R0,AMMO
	CALL SETIPS
	MOV #A_SND,R0
	CALL SCH2
3$:	JMP NEXTFR
MNMOV:	MOV #MNBF,R5
1$:	MOVB (R5),R0
	BEQ 2$
	ASL R0
	CALL @MNPRCS-2(R0)
2$:	ADD #MNRS,R5
	CMP #MNBFE,R5
	BNE 1$
	RET
DPROJ1:	CMP GSTATE,#GSHTWN
	BEQ 3$
	TSTB AMMO
	BEQ 3$
	DECB AMMO
	MOV #MNBFS,R5
1$:	TSTB (R5)
	BNE 2$
	MOVB #3.,(R5)
	CLRB 1.(R5)
	MOV PLX,R0
	MOV PLY,R1
	MOV R0,4.(R5)
	MOV R1,6.(R5)
	MOV PLA,8.(R5)
	CALL CHECKP
	MOVB R2,3.(R5)
	MOV #F_SND,R0
	CALL SCH2
	BR 3$
2$:	ADD #MNRS,R5
	CMP #MNBFX,R5
	BNE 1$
3$:	RET
DPROJ2:	CMPB #1.,(R5)
	BNE 3$
	CMP GSTATE,#GSHTWN
	BEQ 3$
	CALL FRN
	CMP RAN,#3500.
	BHI 3$
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R4,-(SP)
	MOV #MNBFS,R4
1$:	TSTB (R4)
	BNE 2$
	MOVB #4.,(R4)
	CLRB 1.(R4)
	MOV 4.(R5),R0
	MOV 6.(R5),R1
	MOV R0,4.(R4)
	MOV R1,6.(R4)
	CALL CHECKP
	MOVB R2,3.(R4)
	CALL GETANG
	MOV #F_SND,R0
	CALL SCH2
	BR 4$
2$:	ADD #MNRS,R4
	CMP #MNBFX,R4
	BNE 1$
4$:	MOV (SP)+,R4
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
3$:	RET
MNSYNC:	MOV #MNBF,R0
	MOV #SMNBF,R1
1$:	MOV (R0)+,(R1)+
	MOV (R0)+,(R1)+
	MOV (R0)+,(R1)+
	MOV (R0)+,(R1)+
	MOV (R0)+,(R1)+
	MOV (R0)+,(R1)+
	CMP R0,#MNBFE
	BNE 1$
	RET
MNBF:	.BLKB 60.
MNBFS:	.BLKB 180.
MNBFX:	.BLKB 120.
MNBFE:
SMNBF:	.BLKB 60.+180.+120.
SMNBFE:	NOP

GOTIT:	MOV #5,(PC)+
MNUITM:	.WORD 0
	MOV #-1,(PC)+
OLDITM:	.WORD 0
	CALL CLVRAM
	MOV #V_SC1,R0
	CALL SCH2
	MOV #40000,R0
	CLR (R0)+
	TST R0
	BPL .-4
	MOV #TITDAT,R0
	MOV #41400,R1
	CALL UNPK
	CALL CONVSC
	CALL PRVER
	MOV #TX1,R5
	CALL DRTEXT
	CALL KEYRST
MNULP2:	CALL PRNUM
	CALL PRPAL
MNULP:	CALL DRMENU
	CALL FRN
	MOV MNUITM,R1
	TST KeyUp
	BEQ 1$
	CLR KeyUp
	CMP R1,#5
	BEQ MNULP
	INC R1
	MOV R1,MNUITM
	BR MNULP
1$:	TST KeyDown
	BEQ 2$
	CLR KeyDown
	CMP R1,#1
	BEQ MNULP
	DEC R1
	MOV R1,MNUITM
	BR MNULP
2$:	TST KeyEnter
	BEQ MNULP
	CLR KeyEnter
	DEC R1
	BEQ 5$
	DEC R1
	BEQ 6$
	DEC R1
	BEQ 3$
	DEC R1
	BEQ 4$
	CALL CLWHLE
	MOV #V_SC0,R0
	CALL SCH2
	CALL GO
	BR GOTIT
3$:	MOV #TX2,R5
	CALL DRINFO
	MOV #TX3,R5
	CALL DRINFO
	MOV #TX4,R5
	CALL DRINFO
	JMP GOTIT
4$:	INC AMAP
	CMP AMAP,#5
	BLO MNULP2
	CLR AMAP
	JMP MNULP2
5$:
	MOV #F_EXIT,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	MOV OLDTMI,@#100
	MOV OLDTMM,@#102
	CALL PPFREE
	TSTB @#177564
	BPL .-4
	MOV #14,@#177566
	EMT 350
	HALT
6$:	INC APAL
	CMP APAL,#4
	BLO MNULP2
	CLR APAL
	JMP MNULP2
ANYKEY:	CLR KeyAny
	MOV KeyAny,R0
	BEQ .-4
	RET
KEYRST:	CLR KeyAny
	CLR KeyEnter
	CLR KeyUp
	CLR KeyDown
	RET
DRTEXT:	MOV R5,R0
	CALL TXPS
	CMP (R5)+,(R5)+
	MOV R5,R1
	TST (R1)+
	MOV (R5)+,R5
1$:	CALL TXST
	SOB R5,1$
	RET
DRINFO:	CALL CLVRAM
	CALL DRTEXT
	CALL ANYKEY
	RET
INVSTR:	MOV #TX1,R0
	CALL TXPS
	SUB #2,R0
	ASL R5
	ADD MNUADR-2(R5),R0
	MOV R0,@#176640
	MOV #14,R4
2$:	MOV #22,R3
1$:	COM @#176642
	INC @#176640
	COM @#176642
	INC @#176640
	SOB R3,1$
	ADD #C_SCRWIH-44,@#176640
	SOB R4,2$
	RET
MNUADR:	.WORD 7040.-80.,5280.-80.,3520.-80.,1760.-80.,-80.
DRMENU:	CMP OLDITM,MNUITM
	BEQ 1$
	MOV OLDITM,R5
	BMI 2$
	CALL INVSTR
2$:	MOV MNUITM,R5
	CALL INVSTR
1$:	MOV MNUITM,OLDITM
	RET
BEFUPD:	MOV MNUITM,R5
	CMP R5,#4
	BEQ 1$
	CMP R5,#2
	BNE 2$
1$:	CALL INVSTR
2$:	RET
PRNUM:	CALL BEFUPD
	MOV #STRNP,R0
	CALL TXPS
	MOV AMAP,R1
	ADD #61,R1
	MOVB R1,STRN
	MOV #STRN,R1
	CALL TXST
	CALL BEFUPD
	RET
PRPAL:	CALL BEFUPD
	MOV #STRPL,R0
	CALL TXPS
	MOV APAL,R1
	ASL R1
	MOV STRPLO(R1),R1
	CALL TXST
	CALL BEFUPD
	MOV #12.,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	RET
STRPLO:	.WORD STRPP1,STRPP2,STRPP3,STRPP4
PRVER:	MOV #STRVP,R0
	CALL TXPS
	MOV #STRV,R1
	CALL TXST
	RET
TX1:	.WORD 8.,10.,5.
	.ASCII"START GAME"<12><12><0>
	.ASCII"SELECTED ARENA"<12><12><0>
	.ASCII"INFORMATION"<12><12><0>
	.ASCII"PALETTE"<12><12><0>
	.ASCII"EXIT"<0>
	.EVEN
TX2:	.WORD 4.,3.,9.
	.ASCII<237>"LEGEND"<237><12><12><0>
	.ASCII"MONSTERS FROM THE PAST"<12><0>
	.ASCII"FOUND MAGIC PORTAL ON"<12><0>
	.ASCII"UKNC SCHOOL COMPUTERS!"<12><12><0>
	.ASCII"YOU ARE OUR LAST HOPE!"<12><12><0>
	.ASCII"FIGHT THIS EVIL AND MAY"<12><0>
	.ASCII"THE FORCE BE WITH YOU!"<12><12><0>
	.ASCII"YOUR TASK IS TO SCORE"<12><0>
	.ASCII"25 POINTS TO WIN!"<0>
	.EVEN
TX3:	.WORD 4.,2.,11.
	.ASCII<237>"CONTROLS"<237><12><12><0>
	.ASCII"SPACE - FIRE"<12><0>
	.ASCII"ARROWS - TURN LEFT, RIGHT"<12><0>
	.ASCII"ARROWS, C, Y - MOVE"<12><0>
	.ASCII"SHIFT, F, W - SLIDE"<12><12><0>
	.ASCII"ENTER - RESTART"<12><12><0>
	.ASCII"K1 - MAP"<12><0>
	.ASCII"K2 - PAUSE"<12><0>
	.ASCII"K3, J - TURN AROUND"<12><0>
	.ASCII"K5 - EXIT"<12><12><0>
	.ASCII"ALSO YOU CAN USE MOUSE"<12><0>
	.EVEN
TX4:	.WORD 4.,3.,9.
	.ASCII<237>"TEAM"<237><12><12><0>
	.ASCII"GAME:"<12><0>
	.ASCII"APRAKSIN DMITRY (ADW2RU)"<12><12><0>
	.ASCII"TITLE:"<12><0>
	.ASCII"LASHIN URY (iNoArt Studio)"<12><12><0>
	.ASCII"COMMUNITY:"<12><0>
	.ASCII"t.me"<57>"MC0511UKNC"<12><0>
	.ASCII"t.me"<57>"bk0010_11m"<12><12><0>
	.ASCII"15.03.25"<0>
	.EVEN
STRNP:	.WORD 23.,12.
STRN:	.ASCII<0><0>
	.EVEN
STRPL:	.WORD 16.,16.
STRPP1:	.ASCII"GRB"<0>
STRPP2:	.ASCII"RGB"<0>
STRPP3:	.ASCII"BW1"<0>
STRPP4:	.ASCII"BW2"<0>
	.EVEN
STRVP:	.WORD 19.,20.
STRV:	.ASCII<223>"0.8 BETA UKNC"<221><0>
	.EVEN
UNPK:	MOV #-1,R4
	XOR R2,R2
	BR DCL

NXDC:	MOVB (R0)+,(R1)+
DCL:	CALL RB
	BCS NXDC

	MOV #1,R3
OFN:	ADD R3,R3
	CALL RB
	ADC R3
	CALL RB
	BCS OFE
	DEC R3
	ADD R3,R3
	CALL RB
	ADC R3
	BR OFN

RB:	ADD R2,R2
	BEQ RBZ
	RET
RBZ:	MOVB (R0)+,R2
	SWAB R2
	BIC #177,R2
	BIS #200,R2
	ADD R2,R2
	RET

OFE:	SUB #3,R3
	BCC OF2

	MOV R4,R3
	XOR R5,R5
	CALL RB
	ADC R5
	BR OF3

OF2:	SWAB R3
	MOVB (R0)+,R5
	BIC #177400,R5
	BIS R5,R3
	COM R3
	BEQ BRK
	XOR R5,R5
	SEC
	ROR R3
	ADC R5
	MOV R3,R4

OF3:	ADD R5,R5
	CALL RB
	ADC R5
	BNE LF1

	INC R5
NXLF:	ADD R5,R5
	CALL RB
	ADC R5
	CALL RB
	BCC NXLF
	ADD #2,R5

LF1:	CMP R3,#175400
	ADC R5
	INC R5

	ADD R1,R3
NXMV:	MOVB (R3)+,(R1)+
	SOB R5,NXMV
	BR DCL

BRK:	RET
CONVSC:	MOV #40000,R0
	MOV #C_TADDR,R1

	MOV #150.,-(SP)

2$:	MOV #100,R2

1$:	MOVB (R0)+,R3

	CLR R4
	CLR R5

	ASR R3
	ROR R5
	ROR R5

	ASR R3
	ROR R4
	ROR R4

	ASR R3
	ROR R5
	ROR R5

	ASR R3
	ROR R4
	ROR R4

	ASR R3
	ROR R5
	ROR R5

	ASR R3
	ROR R4
	ROR R4

	ASR R3
	ROR R5
	ROR R5

	ASR R3
	ROR R4
	ROR R4

	SWAB R5
	BIS R4,R5

	MOV R5,R4
	ASL R4
	BIS R4,R5

	MOV R1,@#176640
	INC R1
	MOV R5,@#176642

	SOB R2,1$

	ADD #20,R1

	DEC (SP)
	BNE 2$
	TST (SP)+

	RET
TXCH:	MOV R0,PPUV1
	MOV R2,PPUV2
	MOV CHMSK,PPUV3
	MOV CHUND,PPUV4
	MOV #F_PCHR,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	MOV PPUV1,R0
	RET
CHMSK:	.WORD 0
CHUND:	.WORD 0
TXST:	MOV R0,R3
1$:	CLR R2
	BISB (R1)+,R2
	BEQ 2$
	CMPB R2,#12
	BEQ 3$
	CMPB R2,#221
	BEQ 4$
	CMPB R2,#222
	BEQ 5$
	CMPB R2,#223
	BEQ 6$
	CMPB R2,#237
	BEQ 7$
	TSTB R2
	BPL 8$
	SUB #32.,R2
8$:	CALL TXCH
	BR 1$
2$:	RET
3$:	ADD #880.,R3
	MOV R3,R0
	BR 1$
4$:	CLR CHMSK
	BR 1$
5$:	MOV #^B0000000011111111,CHMSK
	BR 1$
6$:	MOV #^B1111111100000000,CHMSK
	BR 1$
7$:	COM CHUND
	BR 1$
TXPS:	MOV (R0)+,-(SP)
	ASL (SP)
	MOV (R0)+,R0
	MOV R0,-(SP)
	ASL R0
	MOV R0,-(SP)
	ASL R0
	ASL R0
	ADD (SP)+,R0
	ADD (SP)+,R0
	ASL R0
	ASL R0
	ASL R0
	ASL R0
	MOV R0,-(SP)
	ASL R0
	ASL R0
	ADD (SP)+,R0
	ADD (SP)+,R0
	ADD #C_TADDR+2400,R0
	RET

TITDAT:	.BYTE 220,0,53,366,240,252,252,2
	.BYTE 103,213,262,252,1,326,110,200
	.BYTE 15,357,250,13,52,157,63,20
	.BYTE 220,177,25,372,377,377,253,264
	.BYTE 0,1,103,25,310,100,377,332
	.BYTE 5,177,41,220,6,140,127,200
	.BYTE 177,34,252,2,206,326,12,12
	.BYTE 61,173,200,137,101,177,173,137
	.BYTE 1,375,101,76,200,377,101,365
	.BYTE 12,201,203,174,177,1,364,234
	.BYTE 27,20,177,3,12,40,202,213
	.BYTE 26,177,162,7,366,166,37,377
	.BYTE 210,160,7,375,302,1,321,177
	.BYTE 114,365,216,177,1,12,177,256
	.BYTE 52,10,163,154,240,12,303,63
	.BYTE 376,355,7,365,375,177,126,305
	.BYTE 1,176,350,7,164,375,52,1
	.BYTE 203,375,340,27,366,177,77,300
	.BYTE 250,177,301,177,14,252,202,70
	.BYTE 175,101,210,377,340,37,125,327
	.BYTE 17,0,374,153,176,25,365,24
	.BYTE 167,250,147,25,240,37,336,140
	.BYTE 365,177,360,72,1,214,60,365
	.BYTE 34,376,133,150,177,315,200,300
	.BYTE 115,3,102,10,177,260,134,211
	.BYTE 350,22,351,322,176,375,143,212
	.BYTE 1,66,374,34,352,236,201,140
	.BYTE 322,176,0,25,171,200,377,76
	.BYTE 177,132,365,101,202,1,177,211
	.BYTE 311,12,200,61,240,364,222,340
	.BYTE 133,153,177,346,125,325,216,374
	.BYTE 201,52,143,370,277,153,201,367
	.BYTE 325,137,152,177,47,377,367,311
	.BYTE 377,15,122,16,176,240,212,377
	.BYTE 143,250,355,174,376,177,155,377
	.BYTE 4,273,176,165,355,377,207,177
	.BYTE 315,340,5,41,127,177,64,377
	.BYTE 203,377,44,10,65,176,323,363
	.BYTE 242,124,173,276,176,137,377,100
	.BYTE 205,210,377,166,137,335,177,212
	.BYTE 361,372,17,170,3,177,325,42
	.BYTE 177,47,52,0,40,73,370,3
	.BYTE 147,217,167,264,137,377,123,370
	.BYTE 104,202,216,367,325,303,177,200
	.BYTE 134,1,124,331,40,146,123,366
	.BYTE 177,3,27,15,377,122,5,377
	.BYTE 60,202,50,373,165,376,27,154
	.BYTE 167,177,5,212,5,41,151,176
	.BYTE 201,50,74,25,140,201,10,41
	.BYTE 177,242,252,377,5,275,0,176
	.BYTE 125,140,377,250,377,77,176,137
	.BYTE 130,177,265,200,177,3,336,310
	.BYTE 177,324,34,7,25,177,134,2
	.BYTE 206,41,372,373,376,37,374,37
	.BYTE 177,2,21,1,24,176,376,7
	.BYTE 336,7,327,177,177,220,0,251
	.BYTE 220,1,70,37,25,377,23,12
	.BYTE 1,217,211,2,240,34,201,141
	.BYTE 173,373,177,134,17,46,171,52
	.BYTE 355,377,377,173,375,7,177,25
	.BYTE 162,370,7,1,202,50,177,241
	.BYTE 220,250,334,10,163,200,37,256
	.BYTE 206,261,203,177,360,135,200,26
	.BYTE 212,201,1,261,17,177,360,15
	.BYTE 140,25,212,201,1,261,137,177
	.BYTE 344,37,100,201,34,177,160,12
	.BYTE 270,177,307,201,342,350,127,25
	.BYTE 362,177,207,177,337,37,303,6
	.BYTE 24,171,24,73,127,126,176,311
	.BYTE 360,362,137,17,126,24,170,5
	.BYTE 126,73,200,363,177,176,177,5
	.BYTE 36,4,10,177,51,25,22,252
	.BYTE 174,177,1,330,201,375,1,76
	.BYTE 5,20,21,266,1,377,2,355
	.BYTE 170,177,301,317,177,17,0,76
	.BYTE 5,41,27,170,274,5,176,77
	.BYTE 234,340,240,33,33,375,16,21
	.BYTE 1,252,177,204,117,376,366,2
	.BYTE 370,247,326,377,277,340,127,201
	.BYTE 375,41,103,340,330,125,125,177
	.BYTE 36,125,151,377,320,377,54,177
	.BYTE 23,375,236,177,77,117,177,125
	.BYTE 303,177,1,377,124,306,52,253
	.BYTE 377,336,12,140,161,375,326,177
	.BYTE 216,17,175,314,363,20,120,1
	.BYTE 123,300,332,3,340,377,377,370
	.BYTE 337,377,167,1,0,30,212,3
	.BYTE 266,177,364,21,371,100,22,230
	.BYTE 361,20,150,377,70,2,40,177
	.BYTE 152,52,205,3,204,377,170,217
	.BYTE 334,1,125,333,370,177,273,37
	.BYTE 345,223,340,337,177,221,357,301
	.BYTE 377,130,334,170,322,377,370,177
	.BYTE 77,134,15,177,26,11,143,103
	.BYTE 246,177,252,1,377,66,337,377
	.BYTE 355,221,220,120,301,21,0,375
	.BYTE 21,216,5,51,207,367,153,145
	.BYTE 377,172,226,5,171,0,366,327
	.BYTE 337,337,176,346,77,0,341,330
	.BYTE 23,1,375,243,35,152,125,336
	.BYTE 365,77,364,372,326,10,1,377
	.BYTE 200,173,177,225,325,176,344,252
	.BYTE 377,20,305,373,61,360,210,304
	.BYTE 30,317,56,326,360,3,300,16
	.BYTE 137,74,307,177,5,235,332,12
	.BYTE 377,251,332,374,3,176,77,153
	.BYTE 77,23,373,360,77,0,377,177
	.BYTE 32,373,207,317,177,2,50,371
	.BYTE 257,374,17,151,177,236,17,3
	.BYTE 10,377,132,177,320,50,175,226
	.BYTE 177,154,377,156,362,177,77,137
	.BYTE 170,125,11,220,326,341,127,266
	.BYTE 373,177,43,175,147,360,17,301
	.BYTE 177,171,337,74,173,300,17,204
	.BYTE 0,337,331,370,127,200,261,125
	.BYTE 177,332,25,377,55,217,177,60
	.BYTE 360,220,377,37,27,0,366,363
	.BYTE 4,375,61,5,121,371,54,177
	.BYTE 171,63,0,3,222,376,324,50
	.BYTE 377,260,155,371,177,315,363,303
	.BYTE 64,77,46,3,101,177,340,365
	.BYTE 360,214,375,164,367,115,317,201
	.BYTE 177,362,317,17,300,363,324,250
	.BYTE 367,357,353,340,142,375,374,260
	.BYTE 175,232,371,55,362,176,377,77
	.BYTE 162,377,72,352,52,57,152,127
	.BYTE 121,377,124,243,125,207,175,45
	.BYTE 125,5,24,363,135,3,305,176
	.BYTE 206,267,177,300,56,376,277,27
	.BYTE 175,366,177,301,177,176,171,0
	.BYTE 0,42,336,177,3,102,140,377
	.BYTE 22,367,370,177,303,77,24,352
	.BYTE 341,175,24,357,134,300,120,162
	.BYTE 45,177,245,176,363,313,60,0
	.BYTE 377,43,17,30,200,151,235,300
	.BYTE 377,240,376,317,272,377,303,153
	.BYTE 206,377,177,10,160,227,70,351
	.BYTE 355,21,177,103,17,14,377,166
	.BYTE 3,177,144,203,366,103,5,377
	.BYTE 10,333,177,310,63,300,135,121
	.BYTE 200,373,76,0,0,360,306,177
	.BYTE 15,213,323,177,227,330,374,177
	.BYTE 365,177,121,300,321,130,173,141
	.BYTE 360,177,7,14,146,20,177,341
	.BYTE 20,20,337,22,322,176,77,273
	.BYTE 151,177,63,360,353,17,374,21
	.BYTE 351,3,374,25,162,374,124,364
	.BYTE 56,13,374,272,176,17,273,0
	.BYTE 77,120,130,241,354,25,60,335
	.BYTE 27,377,143,177,167,1,177,317
	.BYTE 300,317,100,321,125,375,175,211
	.BYTE 301,345,13,177,130,5,176,0
	.BYTE 147,177,0,124,342,125,125,125
	.BYTE 60,335,103,77,4,377,206,202
	.BYTE 160,157,157,377,177,17,0,357
	.BYTE 236,0,175,5,265,0,1,2
	.BYTE 41,331,20,154,1,100,66,20
	.BYTE 373,213,11,52,377
	.EVEN

KeyJ:		.WORD 0
KeyC:		.WORD 0
KeyF:		.WORD 0
KeyY:		.WORD 0
KeyW:		.WORD 0
Key8:		.WORD 0
Key7:		.WORD 0
Key6:		.WORD 0
Key5:		.WORD 0
Key4:		.WORD 0
Key3:		.WORD 0
Key2:		.WORD 0
Key1:		.WORD 0
KeyK5:		.WORD 0
KeyK4:		.WORD 0
KeyK3:		.WORD 0
KeyK2:		.WORD 0
KeyK1:		.WORD 0
KeyStop:	.WORD 0
KeyUst:		.WORD 0
KeyShift:	.WORD 0
KeyEnter:	.WORD 0
KeySpace:	.WORD 0
KeyRight:	.WORD 0
KeyLeft:	.WORD 0
KeyDown:	.WORD 0
KeyUp:		.WORD 0
KeyAny:		.WORD 0
KeyCurrent:	.WORD 0
PPUV1:		.WORD 0
PPUV2:		.WORD 0
PPUV3:		.WORD 0
PPUV4:		.WORD 0
PpuComm:	.WORD 0
MSST:		.WORD 0
APAL:		.WORD 0

NOMEM:	.ASCIZ"? unable to allocate memory in PPU"
	.EVEN

PPMSG:	.WORD PPARR
        .WORD 401

PPARR:	.BYTE 0
PPCMD:	.BYTE 0
	.WORD 32
PPAPP:	.WORD 0
PPACP:	.WORD 0
PPLEN:	.WORD 0

PPSEN:	MOV #PPMSG,R0
	MOV #5,R1
	BR 1$
2$:	MOVB (R0)+,@#176676
1$:	TSTB @#176674
	BPL 1$
	SOB R1,2$
	RETURN

PPRUN:	MOVB #1,PPCMD
	MOV R5,PPACP
	CALL PPSEN
	TSTB PPARR
	BEQ 10$
	MTPS #0
	MOV #NOMEM,R0
	EMT #351
	EMT #350
10$:	MOVB #20,PPCMD
	MOV R5,PPLEN
	MOV R4,PPACP
	CALL PPSEN
	MOVB #30,PPCMD
	CALL PPSEN
	RETURN

PPFREE:	MOVB #2,PPCMD
	MOV #PPUEND-PPUBEG/2,PPACP	; BUG FIX
	CALL PPSEN
	RETURN

PPUBEG:

	MTPS #200
	CALL SAVE
	CALL IVTS
	MOV PC,-(SP)
	ADD #KINT-.,(SP)
	MOV (SP)+,@#300
	BIS #100,@#177700
	MOV #7774,@#7214
	CLR NOMSE
	MOV PC,-(SP)
	ADD #I004-.,(SP)
	MOV (SP)+,@#4
	TST @#177400
	MOV #MSST/2,@#177010
	CLR @#177014
	MOV #256.+1.,PMSST
	MOV PC,-(SP)
	ADD #CH2PLI-.,(SP)
	MOV (SP)+,@#340
	MOV #200,@#342
	BIS #2,@#177066
	BIS #^B0000001100000000,@#177054
	BIC #^B0001111110000000,@#177716
	CALL TINIT
	MOV PC,-(SP)
	ADD #POP-.,(SP)
	MOV (SP)+,@#304
	MOV PC,-(SP)
	ADD #POPT-.,(SP)
	MOV (SP)+,@#100
	MOV #128.,TON+2
	BIS #107,@#177710
	BIC #^B0000000100000000,@#177054
	CALL READY
	MTPS #0
	RETURN

I004:	MOV #1,(PC)+
NOMSE:	.WORD 0
	MOV @#177010,-(SP)
	MOV R0,-(SP)
	MOV (PC)+,R0
FLGC:	.WORD 0
	INC FLGC
	BIC #177700,FLGC
	ADD #51010,R0
	MOV R0,@#177010
	COM @#177014
	MOV (SP)+,R0
	MOV (SP)+,@#177010
	RTI

READY:
	MOV @#177010,-(SP)
	MOV #PpuComm/2,@#177010
	CLR @#177014
	MOV (SP)+,@#177010
	RET

SAVEM:	MOV @#177010,-(SP)
	MOV #40000/2,R0		; FROM
	MOV #100000,R1
	MOV #30880./2,R2	; HOW MANY
1$:	MOV R0,@#177010
	INC R0
	MOV @#177014,R3
	MOV R1,@#177010
	INC R1
	MOV R3,@#177012
	SWAB R3
	MOV R1,@#177010
	INC R1
	MOV R3,@#177012
	SOB R2,1$
	MOV (SP)+,@#177010
	RET

RESTM:	MOV @#177010,-(SP)
	MOV #40000/2,R0		; INTO
	MOV #100000,R1
	MOV #30880./2,R2	; HOW MANY
1$:	MOV R1,@#177010
	INC R1
	MOV @#177012,R3
	SWAB R3
	MOV R1,@#177010
	INC R1
	BIS @#177012,R3
	MOV R0,@#177010
	INC R0
	SWAB R3
	MOV R3,@#177014
	SOB R2,1$
	MOV (SP)+,@#177010
	RET

OLD004:	.WORD 0
OLD272:	.WORD 0
OLD100:	.WORD 0
OLD304:	.WORD 0
OLD340:	.WORD 0
OLD342:	.WORD 0
OLD300:	.WORD 0
OL7214:	.WORD 0
OLD010: .WORD 0
OLDX00: .WORD 0
OLDX66: .WORD 0
OLDX16: .WORD 0
OLDX54:	.WORD 0

SAVE:	MOV @#4,OLD004
        MOV @#272,OLD272
	MOV @#100,OLD100
	MOV @#304,OLD304
	MOV @#340,OLD340
	MOV @#342,OLD342
	MOV @#300,OLD300
	MOV @#7214,OL7214
	MOV @#177010,OLD010
	MOV @#177700,OLDX00
	MOV @#177066,OLDX66
	MOV @#177716,OLDX16
	MOV @#177054,OLDX54
	CALL SAVEM
	RETURN

REST:	CALL RESTM
	MOV OLDX54,@#177054
	MOV OLDX16,@#177716
	MOV OLDX66,@#177066
	MOV OLDX00,@#177700
	CLR @#177712
	MOV OLD010,@#177010
	MOV OL7214,@#7214
	MOV OLD300,@#300
	MOV OLD340,@#340
	MOV OLD342,@#342
	MOV OLD304,@#304
	MOV OLD100,@#100
	MOV OLD272,@#272
	MOV OLD004,@#4
	RETURN
VT1A:	.WORD 0
VT2A:	.WORD 0
VT3A:	.WORD 0
IVTS:	MOV PC,R0
	ADD #VTABS-.,R0
	ADD #7,R0
	BIC #7,R0
	MOV R0,VT1A
	MOV #114610,(R0)+
	MOV #125314,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)
	BIS #2,(R0)+
	MOV #24237,(R0)+
	MOV #7,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV #16.,R1
1$:     CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,1$
	MOV #16.,R1
2$:     MOV #51000,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,2$
	MOV #20000,R2
	MOV #100000,R3
	MOV #64.,R1
3$:     MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	SOB R1,3$
	MOV #44000,R3
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	BIS #1,-2(R0)
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	BIS #2,-2(R0)
	MOV #24017,(R0)+
	MOV #7,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	BIS #2,-2(R0)
	MOV #24237,(R0)+
	MOV #7,(R0)+
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	BIS #1,-2(R0)
	MOV #64.-4.,R1
4$:     MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	BIT R1,#1
	BEQ 40$
	ADD #80.,R3
40$:
	SOB R1,4$
	MOV #51000,(R0)+
	MOV R0,(R0)
	SUB #2,(R0)+
	ADD #7,R0
	BIC #7,R0
	MOV R0,VT2A
	MOV #114610,(R0)+
	MOV #125314,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)
	BIS #2,(R0)+
	MOV #20,(R0)+
	MOV #7,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV #16.,R1
5$:     CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,5$
	MOV #16.,R1
6$:     MOV #51000,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,6$
	MOV #256.,R1
	MOV #100000,R2
7$:	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	SOB R1,7$
	MOV #51000,(R0)+
	MOV R0,(R0)
	SUB #2,(R0)+
	ADD #7,R0
	BIC #7,R0
	MOV R0,VT3A
	MOV #114610,(R0)+
	MOV #125314,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)
	BIS #2,(R0)+
	MOV #24237,(R0)+
	MOV #7,(R0)+
	CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV #16.,R1
8$:     CLR (R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,8$
	MOV #16.,R1
9$:     MOV #51000,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	SOB R1,9$
	MOV #20000,R2
	MOV #100000,R3
	MOV #64.,R1
10$:	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	SOB R1,10$
	MOV #51120,R3
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	BIS #1,-2(R0)
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	BIS #2,-2(R0)
	MOV #24017,(R0)+
	MOV #7,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	BIS #2,-2(R0)
	MOV #24237,(R0)+
	MOV #7,(R0)+
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R3
	BIS #1,-2(R0)
	MOV #64.-4.,R1
11$:	MOV R2,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	ADD #80.,R2
	MOV R3,(R0)+
	MOV R0,(R0)
	ADD #2,(R0)+
	BIT R1,#1
	BEQ 110$
	ADD #80.,R3
110$:
	SOB R1,11$
	MOV #51000,(R0)+
	MOV R0,(R0)
	SUB #2,(R0)+
	RETURN

CH2PLI:	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	MOVB @#177064,R0
	ASL R0
	ADD R0,PC
	BR CH2P0
	BR CH2P1
	BR CH2P2
	BR CH2P3
	BR CH2P4
	BR CH2P5
	BR CH2P6
	BR CH2P7
	BR CH2P8
	BR CH2P9
	BR CH2P10
	BR CH2P11
	BR CH2P12
EXP:	CALL READY
	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	RTI

CH2P0:
	MOV VT1A,-(SP)
	BIS #6,(SP)
	MOV (SP)+,@#272
	BR EXP
CH2P1:
	MOV VT2A,-(SP)
	BIS #6,(SP)
	MOV (SP)+,@#272
	BR EXP
CH2P2:
	MOV #478.,TON+2
	MOV #5.,COUNT
	BR EXP
CH2P3:
	MOV #379.,TON+2
	MOV #5.,COUNT
	BR EXP
CH2P4:
	MOV #239.,TON+2
	MOV #5.,COUNT
	BR EXP
CH2P5:	CALL IVTS
	BR EXP
CH2P6:	CALL REST
	BR EXP
CH2P7:  CALL XTXCH
	BR EXP
CH2P8:  CALL XPRCH
	BR EXP
CH2P9:	BIC #^B0000001000000000,@#177054
	BR EXP
CH2P10:	MOV VT3A,-(SP)
	BIS #6,(SP)
	MOV (SP)+,@#272
	BR EXP
CH2P11:	CALL MSSPR
	BR EXP
CH2P12: MOV @#177010,-(SP)
	MOV #APAL/2,@#177010
	MOV @#177014,R0
	ASL R0
	ASL R0
	MOV PC,R1
	ADD #PALTAB-.,R1
	ADD R0,R1
	MOV (R1)+,R0
	MOV (R1),R1
	MOV VT1A,R2
	MOV R0,(R2)+
	MOV R1,(R2)
	MOV VT2A,R2
	MOV R0,(R2)+
	MOV R1,(R2)
	MOV VT3A,R2
	MOV R0,(R2)+
	MOV R1,(R2)
	MOV (SP)+,@#177010
	BR EXP
PALTAB:	.WORD 114610,125314
	.WORD 114610,146252
	.WORD 135610,177756
	.WORD 156610,177756

POPT:	DEC (PC)+
COUNT:	.WORD 5.
	BNE 1$
	MOV #128.,TON+2
	MOV #5.,COUNT
1$:	CALL MSSPR
	RTI

POP:	TST @#177714
	TST @#177714
	CMP TON+2,#128.
	BEQ TON
	MOV R0,-(SP)
	MOV #200,R0
	XOR R0,@#177716
	MOV (SP)+,R0
TON:	MOV #0,@#177712
	RTI

MSSPR:	TST NOMSE
	BNE 1$
	MOV R0,-(SP)
	MOV @#177400,R0
	MOV @#177010,-(SP)
	MOV #MSST/2,@#177010
	TST NOMSE
	BEQ 6$
	CLR NOMSE
	CLR R0
6$:	CMP R0,#100601
	BNE 7$
	CLR R0
7$:	MOV R0,@#177014
	BIT R0,#1.
	BEQ 2$
	BIT PMSST,#1.
	BNE 4$
	MOV #KeyShift/2,@#177010
	INC @#177014
	BR 4$
2$:	BIT PMSST,#1.
	BEQ 4$
	MOV #KeyShift/2,@#177010
	CLR @#177014
4$:	BIT R0,#256.
	BEQ 3$
	BIT PMSST,#256.
	BNE 5$
	MOV #KeySpace/2,@#177010
	INC @#177014
	BR 5$
3$:	BIT PMSST,#256.
	BEQ 5$
	MOV #KeySpace/2,@#177010
	CLR @#177014
5$:	MOV R0,PMSST
	MOV (SP)+,@#177010
	MOV (SP)+,R0
1$:	RET

PMSST:	.WORD 0

TINIT:	CLR @#177710
	TST @#177714
	TST @#177714
	TST @#177710
	BNE TINIT
	RETURN

KINT:	MOV R0,-(SP)
	MOV @#177702,R0
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	MOV #177010,R4
	MOV #177014,R5
	MOV (R4),-(SP)
	MOV #KeyCurrent/2,(R4)
	MOV R0,(R5)
	BIT #^B10000000,R0
	BNE 50$
	MOV #KeyAny/2,(R4)	; 'anykey' is also pressed
	INC (R5)
	MOV PC,R1
	ADD #KDN-.,R1
10$:	TST (R1)
	BEQ 90$
	CMP R0,(R1)+
	BEQ 20$
	ADD #4,R1
	BR 10$
20$:
	MOV (R1)+,(R4)
	INC (R5)
	MOV (R1)+,(R4)
	BEQ 90$
	CLR (R5)
	BR 90$
50$:
	BIC #^B1111111111110000,R0
	ASL R0
	ADD R0,PC
	BR 90$
	BR 90$
	BR 90$
	BR 90$
	BR 104$
	BR 105$
	BR 90$
	BR 107$
	BR 108$
	BR 109$
	BR 110$
	BR 111$
	BR 112$
	BR 113$
	BR 114$
	BR 115$
104$:	MOV #KeyStop/2,(R4)
	BR 91$
105$:   MOV #KeyShift/2,(R4)
	BR 91$
107$:	MOV #KeyJ/2,(R4)
	CLR (R5)
	MOV #KeyF/2,(R4)
	BR 91$
108$:   MOV #KeyC/2,(R4)
	CLR (R5)
	MOV #KeyY/2,(R4)
	CLR (R5)
	MOV #Key1/2,(R4)
	CLR (R5)
	MOV #KeyK1/2,(R4)
	BR 91$
109$:   MOV #KeyW/2,(R4)
	CLR (R5)
	MOV #Key2/2,(R4)
	CLR (R5)
	MOV #KeyK2/2,(R4)
	BR 91$
110$:   MOV #Key3/2,(R4)
	CLR (R5)
	MOV #KeyUst/2,(R4)
	CLR (R5)
	MOV #KeyK3/2,(R4)
	BR 91$
111$:   MOV #Key4/2,(R4)
	CLR (R5)
	MOV #KeySpace/2,(R4)
	CLR (R5)
	MOV #KeyRight/2,(R4)
	BR 91$
112$:   MOV #Key5/2,(R4)
	CLR (R5)
	MOV #KeyDown/2,(R4)
	CLR (R5)
	MOV #KeyUp/2,(R4)
	CLR (R5)
	MOV #KeyK4/2,(R4)
	BR 91$
113$:   MOV #Key6/2,(R4)
	CLR (R5)
	MOV #KeyK5/2,(R4)
	BR 91$
114$:   MOV #Key7/2,(R4)
	CLR (R5)
	MOV #KeyLeft/2,(R4)
	BR 91$
115$:   MOV #Key8/2,(R4)
91$:	CLR (R5)
90$:	MOV (SP)+,(R4)
	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	RTI
KDN:	.WORD ^B00000100,KeyStop/2,0
	.WORD ^B01101010,KeyUst/2,0
	.WORD ^B01101011,KeyEnter/2,0
	.WORD ^B00011000,Key1/2,0
	.WORD ^B00011001,Key2/2,0
	.WORD ^B00011010,Key3/2,0
	.WORD ^B00001011,Key4/2,0
	.WORD ^B00011100,Key5/2,0
	.WORD ^B00011101,Key6/2,0
	.WORD ^B00001110,Key7/2,0
	.WORD ^B00001111,Key8/2,0
	.WORD ^B01001011,KeySpace/2,0
	.WORD ^B01011100,KeyDown/2,KeyUp/2
	.WORD ^B01101100,KeyUp/2,KeyDown/2
	.WORD ^B01001110,KeyLeft/2,KeyRight/2
	.WORD ^B01011011,KeyRight/2,KeyLeft/2
	.WORD ^B00001000,KeyK1/2,0
	.WORD ^B00001001,KeyK2/2,0
	.WORD ^B00001010,KeyK3/2,0
	.WORD ^B00001100,KeyK4/2,0
	.WORD ^B00001101,KeyK5/2,0
	.WORD ^B01000101,KeyShift/2,0
	.WORD ^B00010111,KeyJ/2,KeyK3/2
	.WORD ^B00101000,KeyC/2,KeyDown/2
	.WORD ^B00100111,KeyF/2,KeyW/2
	.WORD ^B00111000,KeyY/2,KeyUp/2
	.WORD ^B00111001,KeyW/2,KeyF/2
	.WORD 0

XTXCH:	MOV @#177010,-(SP)
	MOV #PPUV1/2,@#177010
	MOV @#177014,R0
	MOV #PPUV2/2,@#177010
	MOV @#177014,R2
	MOV #PPUV3/2,@#177010
	MOV @#177014,XCHMSK
	MOV #PPUV4/2,@#177010
	MOV @#177014,XCHUND
	MOV R2,-(SP)
	ASL R2
	MOV R2,-(SP)
	ASL R2
	ASL R2
	ADD (SP)+,R2
	ADD (SP)+,R2
	ADD #FNT,R2
	MOV #11.,R1
1$:	MOVB (R2)+,R3
	CLR R4
	CLR R5
	RORB R3
	BCC 2$
	BIS #^B0000001100000011,R4
2$:	RORB R3
	BCC 3$
	BIS #^B0000110000001100,R4
3$:	RORB R3
	BCC 4$
	BIS #^B0011000000110000,R4
4$:	RORB R3
	BCC 5$
	BIS #^B1100000011000000,R4
5$:	RORB R3
	BCC 6$
	BIS #^B0000001100000011,R5
6$:	RORB R3
	BCC 7$
	BIS #^B0000110000001100,R5
7$:	RORB R3
	BCC 8$
	BIS #^B0011000000110000,R5
8$:	RORB R3
	BCC 9$
	BIS #^B1100000011000000,R5
9$:
	BIC XCHMSK,R4
	BIC XCHMSK,R5
	MOV R0,@#177010
	MOV R4,@#177014

	INC @#177010
	MOV R5,@#177014

	ADD #C_SCRWIH,R0
	SOB R1,1$
	DEC @#177010
	BIS XCHUND,@#177014
	INC @#177010
	BIS XCHUND,@#177014
	SUB #880.-2.,R0
	MOV #PPUV1/2,@#177010
	MOV R0,@#177014
	MOV (SP)+,@#177010
	RET
XCHMSK:	.WORD 0
XCHUND:	.WORD 0
XPRCH:	MOV @#177010,-(SP)
	MOV #PPUV1/2,@#177010
	MOV @#177014,R0
	MOV #PPUV2/2,@#177010
	MOV @#177014,R1
	MOV R1,-(SP)
	MOV R0,-(SP)
	ASL R0
	MOV R0,-(SP)
	ASL R0
	ASL R0
	ADD (SP)+,R0
	ADD (SP)+,R0
	ADD #FNT,R0
	MOV #11.,-(SP)
1$:	MOV #4.,R2
	MOVB (R0)+,R3
2$:	MOV R1,@#177010
	CLR @#177014
	ADD #C_SCRWIH,@#177010
	CLR @#177014
	RORB R3
	BCC 3$
	MOV R1,@#177010
	BIS #7417,@#177014
	ADD #C_SCRWIH,@#177010
	BIS #7417,@#177014
3$:	RORB R3
	BCC 4$
	MOV R1,@#177010
	BIS #170360,@#177014
	ADD #C_SCRWIH,@#177010
	BIS #170360,@#177014
4$:	INC R1
	SOB R2,2$
	ADD #C_SCRWIH+C_SCRWIH-4,R1
	DEC (SP)
	BNE 1$
	TST (SP)+
	MOV (SP)+,R1
	ADD #4.,R1
	MOV #PPUV2/2,@#177010
	MOV R1,@#177014
	MOV (SP)+,@#177010
	RET

VTABS:	.BLKB 3540.
	NOP

PPUEND:

	.END START

