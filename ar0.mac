	;
	CLR BITFLD
	CLR AMAP
	;
	; CLEAN ALL SCREENS AND BUFFERS
	; INCLUDING EMPTY LINE
	CALL CLWHLE
	;
	; SET TIMER
	MOV @#100,(PC)+
OLDTMI:	.WORD 0
	MOV @#102,(PC)+
OLDTMM:	.WORD 0
	MOV #NEWTMI,@#100
	MOV #200,@#102
	;
	; TURN ON TIMER IN CPU
	MOV #F_TMR,R0
	CALL SCH2
	TST @#PpuComm
	BNE .-4
	;
	MTPS #0
	;
	JMP GOTIT
	;
	; GAME LOGIC THREAD
NEWTMI:	BIT BITFLD,#8.		; IS ACTIVE?
	BEQ 1$			; NO->DO NOTHING
	BIT SKIP,#1		; 1/2
	BNE 1$
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	MOV @#176640,-(SP)
	CALL GLOGIC		; PROCESS PLAYER&MONSTERS CONTROL
	MOV (SP)+,@#176640
	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
1$:	INC (PC)+
SKIP:	.WORD 0
	CMP SKIP,#50.
	BLO 2$
	CLR SKIP
	BIS #128.,BITFLD
2$:	RTI
	;
	; UKNC SEND CMD TO CHANNEL 2
	; IN: R0=COMMAND (LSB)
SCH2:	MFPS -(SP)
	MTPS #200
	TST @#PpuComm
	BNE .-4
	MOV #1,@#PpuComm
	BIT #128.,@#176674
	BEQ .-6
	MOVB R0,@#176676
	BIT #128.,@#176674
	BEQ .-6
	MTPS (SP)+
	RET
	;
	; CLEAN WHOLE SCREEN(S)
CLWHLE:	MOV #40000,R0
	MOV #15440.,R3	; (128+32+1+32)*80
	CLR (R0)+
	SOB R3,.-2
	; CLEAN VIDEO RAM
CLVRAM:	MOV #100000,@#176640
	MOV #20480.,R3	; 256*80
1$:	CLR @#176642
	INC @#176640
	SOB R3,1$
	RET
	;
	; VARIABLES
	;
GSTATE:	.WORD 0		; GAME STATE
	;
	; 0=EXIT
	; 1=GAME RUNNING
	; 2=PAUSED
	; 3=THE	PLAYER WON
	; 4=THE	HUNTERS	WON
	;
BITFLD:	.WORD 0		; CONTROL BITS
	;
AMMO:	.BYTE 10.	; GAME AMMO&OUTPUT
OLDAM:	.BYTE 1.
SCORE:	.BYTE 0.,0.	; GAME SCORE&OUTPUT
OLDSCR:	.BYTE 1.,0.
FRCNTR:	.BYTE 0		; FRAME	COUNTER
	.EVEN
	;
	; PLAYER VARS (WILL BE LOADED WITH MAP)
PLX:	.WORD 0
PLY:	.WORD 0
PLA:	.WORD 0
	;
SPLX:	.WORD 0
SPLY:	.WORD 0
SPLA:	.WORD 0
	;
PROCSK:	.WORD KeyLeft,KeyRight,KeyUp,KeyDown,KeySpace
	.WORD KeyJ,KeyF,KeyC,KeyW,KeyY
	.WORD KeyK1,KeyK2,KeyK3,KeyK5,KeyEnter,KeyStop
PROCSP:	.WORD DLFT,DRGHT,DUP,DDWN,DSPC
	.WORD DROT,DSL,DUP,DSR,DDWN
	.WORD DTAB,DPAUS,DROT,DEXIT,DREST,DFPS
PROCSR:	.WORD 177772,177772,177772,177772,177772
	.WORD 177772,177772,177772,177772,177772
	.WORD 177760,177774,177772,177760,177763,177772
PROCSC:	.WORD 0,0,0,0,1
	.WORD 1,0,0,0,0
	.WORD 1,1,1,1,1,1
PROCST:	.WORD 2,2,10,10,0
	.WORD 0,4,10,4,10
	.WORD 0,0,0,0,0,0
	;
WASP:	.WORD 0	; WAS PROCESSED FLAGS
ANGV:	.WORD 0	; ANGLE VELOSITY
	;
	; KEYBOARD
JKB:	CLR R0
1$:	MOV PROCSK(R0),R1
	TST (R1)
	BEQ 2$
	TST PROCSC(R0)
	BEQ 3$
	CLR (R1)
3$:	MOV GSTATE,R1
	BIC PROCSR(R0),R1
	BEQ 2$
	BIS PROCST(R0),WASP	; SOLVE CONFLICTS KEYBRD VS MOUSE
	MOV PROCSP(R0),R1
	MOV R0,-(SP)
	CALL (R1)
	MOV (SP)+,R0
2$:	TST (R0)+
	CMP R0,#32.
	BNE 1$
	RET
	;
	; MOUSE: FILTER
FMS:	MOV FMSP,R2
	INC R2
	BIC #177774,R2
	MOV R2,FMSP
	MOVB R0,FMSD1(R2)
	MOVB R1,FMSD2(R2)
	CLR R0
	CLR R1
	MOV #4.,R2
1$:	MOVB FMSD1-1(R2),R3
	ADD R3,R0
	MOVB FMSD2-1(R2),R3
	ADD R3,R1
	SOB R2,1$
	ASR R0
	ASR R0
	ASR R0
	ASR R1
	ASR R1
	ASR R1
	RET
FMSP:	.WORD 0
FMSD1:	.BLKB 4.
FMSD2:	.BLKB 4.
	.EVEN
	;
	; MAKE MOUSE MOVE SERVICE FUNCTION
MAKEMM:	TST R1
	BEQ 1$		; NO MOVE IF 0
	BPL 2$
	NEG R1
	MOV R4,R5
2$:	CMP R1,#8.	; 1..8
	BLOS 3$
	MOV #8.,R1
3$:	MOV R1,-(SP)     
	CALL GETDIR
	MOV #9.,R2
	SUB (SP)+,R2	; 8..1
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R0,-(SP)
	MOV R2,-(SP)
	MOV (SP)+,R4
	MOV (SP)+,R2	; R0
	BPL 11$
	NEG R2
	CALL SPDIV
	NEG R2
	BR 12$
11$:	CALL SPDIV
12$:	MOV R2,R0
	MOV (SP)+,R4
	MOV (SP)+,R2	; R1
	BPL 21$
	NEG R2
	CALL SPDIV
	NEG R2
	BR 22$
21$:	CALL SPDIV
22$:	MOV R2,R1
	CALL (R5)
1$:	RET
	;
	; MOUSE: MOVE,ROTATE,SLIDE
JMS:	MOV GSTATE,R1
	BIC PROCSR,R1	; L,R,U,D - SAME RULE
	BEQ 4$
	; GET MOUSE STATUS AND FILTER IT
        MOV MSST,R1
	BIC #256.+1.,R1
	MOV R1,R0
	SWAB R0
	ASRB R0
	ASRB R1
	CALL FMS
	; ROTATE
	MOV R0,-(SP)
	TST KeyShift
	BNE 22$
	BIT #2,WASP	; SOLVE CONFLICT
	BNE 22$
	ADD R1,PLA	; DO ROTATE
	BIC #177000,PLA
	BR 21$
22$:	BIT #4,WASP	; SOLVE CONFLICT
	BNE 21$
	MOV #DSL2,R4	; DO SLIDE(S)
	MOV #DSR2,R5
	CALL MAKEMM
21$:	MOV (SP)+,R1	; R0->R1
	; MOVE
	BIT #10,WASP	; SOLVE CONFLICT
	BNE 4$
	MOV #DDWN2,R4	; DO MOVE(S)
	MOV #DUP2,R5
	CALL MAKEMM
4$:	RET
	;
	; POST OPERATIONS
JPS:	BIT WASP,#1
	BNE 1$
	CLR ANGV
1$:	RET
	;
;	; READ MOUSE STATUS
;RMSST:	MOV #M_STA,R0
;	CALL SCH2
;	TST @#PpuComm
;	BNE .-4
;	RET
	;
	; CONTROLS PROCESSING
JOY:	CLR WASP
	;CALL RMSST
	CALL JKB
	CALL JMS
	CALL JPS
	RET
	;
	; INC FRAME COUNTER
	; USED IN VISIBILITY MAP
	; SHOULD BE 0,2,4...
IFRC:	MOVB FRCNTR,R0
	INCB R0
	INCB R0
	MOVB R0,FRCNTR
	BNE 1$
	CALL CLVIS	; CLEAR	VISIBILITY MAP
1$:	RET
	;
	; INTERNAL GAME LOGIC
GLOGIC:	CMP #GSPAUS,GSTATE
	BEQ 1$
	CALL MNMOV	; MONSTERS MOVE
1$:	CALL JOY	; INPUT&PLAYER MOVE
	CMP #GSHTWN,GSTATE
	BNE 2$
	CALL SETTA
	MOV MNBF+6.,R1
	MOV MNBF+4.,R0
	CALL SETPL
2$:	RET
	;
	; THREAD SYNC
THSNC:	MTPS #200
	; PLAYER SYNC
	MOV PLA,SPLA
	MOV PLX,SPLX
	MOV PLY,SPLY
	; MONSTERS SYNC
	CALL MNSYNC
	MTPS #0
	RET
	;
	; LOAD MAP
	; R0=MAP NUMBER	0..N
LMAP:	CLRB FRCNTR	; FRAME	COUNTER
	CALL PREPM	; PREPARE RUNTIME MAP &	PLAYER POSITION
	CALL CLVIS	; CLEAR	VISIBILITY MAP
	CALL MNSET	; SET MONSTERS
	CALL PREPR	; PREPARE RENDER OUTPUT
	CALL INISCR	; RESET	SCORE
	CALL INIAM	; RESET	AMMO
	RET
	;
GO:	; GAME STARTS HERE
	CALL PREP0	; MAKE UNROLLED	CODE
	CALL CLPSC	; CALC PRESCALLERS
	CALL DRST2	; RESTART LEVEL
	BIS #8.,BITFLD	; TIMER PROCESS ON
	;CALL RMSST
	;
	; MAIN LOOP
ML:	CALL THSNC	; SYNC LOGIC&DRAW THREADS
	CALL IFRC	; FOR VISIBILITY MAP
	CALL RENDR	; RENDER WALLS
	CALL MNPRC	; DRAW MONSTERS
	CALL DRDIFF	; DRAW WALLS INTO SCREEN
	CALL PRNSCR	; PRINT	SCORE
	CALL PRNAM	; PRINT	AMMO
	INC (PC)+
CFPS:	.WORD 0
	CALL PRFPS	; FPS
	;
ML2:	;CALL GLOGIC	; PROCESS PLAYER&MONSTERS CONTROL
	CALL DOMAP	; PROCESS MAP
	CALL DORST	; PROCESS RESTART
	CALL DOMSG	; PROCESS MESSAGES
	MOV GSTATE,R0	; CHECK	GAME STATE
	BEQ 1$		; 0=EXIT
	ASR R0
	BEQ ML		; 1=GAME RUNNING
	ASR R0
	BEQ ML2		; 2=GAME PAUSED
	ASR R0
	BEQ ML		; 4=PLAYER WON
	BR ML		; AND GO BACK TO MAIN LOOP
1$:	BIC #8.,BITFLD	; TIMER PROCESS OFF
	RET		; GAME EXIT
	;
PRFPS:	BIT #128.,BITFLD
	BEQ 1$
	BIC #128.,BITFLD
	BIT #256.,BITFLD
	BEQ 2$
	MOV #100000,R1
	MOV CFPS,R4
	CALL DOUT
2$:	CLR CFPS
1$:	RET
	;
	; DO RESTART
DORST:	BIT #4,BITFLD	; CHECK	FOR RESTART PRESSED
	BEQ 1$
	BIC #4,BITFLD	; CLEAR	FLAG
	BIC #8.,BITFLD	; TIMER PROCESS OFF
	CALL DRST2
	BIS #8.,BITFLD	; TIMER PROCESS ON
	;CALL RMSST
1$:	RET
	;
	; PROCESS MAP
DOMAP:	BIT #1,BITFLD	; CHECK	FOR TAB	PRESSED
	BEQ 2$		; NOT PRESSED
	BIC #1,BITFLD	; CLEAR	PRESSED	FLAG
	BIT #2,BITFLD	; CHECK	STATE
	BNE 1$		; NEED DRAW
	CALL CLRM	; NEED CLEAR
	RET
1$:	CALL DRAWM
	CALL MNDRW	; DRAW MONSTERS&PLAYER ON MAP
	RET
2$:	BIT #2,BITFLD	; NEED DRAW MAP?
	BEQ 3$
	CMP #GSPAUS,GSTATE
	BEQ 3$
	CALL MNDRW	; DRAW MONSTERS&PLAYER ON MAP
3$:	RET
	;
	; PROCESS MESSAGES
DOMSG:	BIT #16.,BITFLD
	BEQ 1$
	BIC #16.,BITFLD
	MOV #2,R5
	CALL PRNMS
	MOV #3,R5
	CALL PRNMS
1$:	BIT #32.,BITFLD
	BEQ 2$
	BIC #32.,BITFLD
	MOV #1,R5
	CALL PRNMS
	MOV #3,R5
	CALL PRNMS
2$:	BIT #64.,BITFLD
	BEQ 3$
	BIC #64.,BITFLD
	CLR R5
	CALL PRNMS
3$:	RET
	;
	; DO KT	(EXIT)
DEXIT:	CLR GSTATE
	RET
	;
	; DO TAB
DTAB:	MOV #2,R0
	MOV BITFLD,R1
	XOR R0,R1
	MOV R1,BITFLD	; SWITCH TAB FLAG
	BIS #1,BITFLD	; SET TAB PRESSED FLAG
1$:	RET
	;
DFPS:	MOV #256.,R0	; SHOW/HIDE FPS
	MOV BITFLD,R1
	XOR R0,R1
	MOV R1,BITFLD
	BIT #256.,R1
	BNE 1$
	;
	; CLEAR FPS MSG
	MOV #100000,@#176640
	MOV #32.,R1
3$:	MOV #8.,R0
2$:	CLR @#176642
	INC @#176640
	SOB R0,2$
	ADD #80.-8.,@#176640
	SOB R1,3$
	;
1$:	RET
	;
	; DO SPACE
DSPC:	CALL DPROJ1
	RET
	;
	; SET PLAYER
SETPL:	MOV R0,PLX
	MOV R1,PLY
	RET
	;
	; GET PLAYER DIRECTION
	; OUT: R0=VX,R1=VY
GETDIR:	MOV PLA,R0
	CALL GSNCS
	RET
	;
	; DO DOWN
DDWN:	CALL GETDIR
DDWN2:	NEG R0
	NEG R1
	BR DUP2
	;
	; DO UP
DUP:	CALL GETDIR
DUP2:	MOV R0,R2
	MOV R1,R3
	ADD PLX,R0
	ADD PLY,R1
	CALL CHECK
	BNE 1$
	CALL SETPL
	BR 3$
1$:	SUB R3,R1
	CALL CHECK
	BNE 2$
	CALL SETPL
	BR 3$
2$:	ADD R3,R1
	SUB R2,R0
	CALL CHECK
	BNE 3$
	CALL SETPL
3$:	RET
	;
	; DO SHIFT LEFT
DSL:	CALL GETDIR
DSL2:	XOR R0,R1
	XOR R1,R0
	XOR R0,R1
	NEG R1
	JMP DUP2
	;
	; DO SHIFT RIGHT
DSR:	CALL GETDIR
DSR2:	XOR R0,R1
	XOR R1,R0
	XOR R0,R1
	NEG R1
	JMP DDWN2
	;
	; INC ANGV: LIMIT 0..6
IANGV:	MOV ANGV,R0
	INC R0
	CMP R0,#6.
	BLO 1$
	MOV #5.,R0
1$:	MOV R0,ANGV
	ASL R0
	MOV IAVV(R0),R0
	BIS #1,WASP
	RET
IAVV:	.WORD 0.,3.,5.,6.,7.,8.
	;	
	; DO LEFT
DLFT:	TST KeyShift
	BNE DSL
	CALL IANGV
	SUB R0,PLA
	BIC #177000,PLA
	RET
	;
	; DO RIGHT
DRGHT:	TST KeyShift
	BNE DSR
	CALL IANGV
	ADD R0,PLA
	BIC #177000,PLA
	RET
	;
	; DO 180 ROTATE
DROT:	ADD #256.,PLA
	BIC #177000,PLA
	RET
	;
	; DO PAUSE
DPAUS:	CMP GSTATE,#GSGAME
	BNE 2$
	; GAME RUNNING->PAUSE IT
	MOV #GSPAUS,GSTATE	; PAUSE
	;CLR R5
	;CALL PRNMS
	BIS #64.,BITFLD
	BR 1$
2$:	CMP GSTATE,#GSPAUS
	BNE 1$
	; GAME PAUSED->RUN IT
	MOV #GSGAME,GSTATE	; RUNNING
	CALL CLMS
1$:	RET
	;
	; DO RESTART GAME
DREST:	CMP GSTATE,#GSPLWN
	BEQ 1$
	CMP GSTATE,#GSHTWN
	BEQ 1$
	RET
1$:	BIS #4,BITFLD		; SET FLAG TO RESTART
	RET
	;
DRST2:	CLR CFPS
	MOV (PC)+,R0
AMAP:	.WORD 0
	CALL LMAP		; LOAD MAP
	MOV #GSGAME,GSTATE	; RUNNING
	CALL CLMS
	BIT #2,BITFLD		; IF MAP VISIBLE THEN REDRAW
	BEQ 1$
	CALL CLRM
	CALL DRAWM
1$:	CLR WASP
	CLR ANGV
	RET
	;
